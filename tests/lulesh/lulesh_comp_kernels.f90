! ============================================================================
! VENDORED THIRD-PARTY FIXTURE -- NOT covered by the dace-fortran BSD license.
! GPL-v3 (AWE Crown Copyright 2014), included ONLY as a dace-fortran inliner
! test fixture.  Upstream:
!   https://github.com/ludgerpaehler/LULESH-Fortran (lulesh_comp_kernels.f90)
!
! MODIFICATIONS by the dace-fortran authors (GPL section 5 -- marking changes),
! made only to reach standards-conforming Fortran:
!   * `m_nodeElemCornerList` changed from rank-2 to rank-1 (canonical LULESH).
!   * `AllocateNodeElemIndexes` rewritten to the canonical node->element
!     corner-list build (the upstream loop was explicitly marked broken).
!   * The two force-gather consumers (`IntegrateStressForElems`,
!     `CalcFBHourglassForceForElems`) rewritten to index the rank-1 corner list
!     `[nodeElemStart(g) : +count]` directly (was an invalid rank-2 pointer
!     bind to a non-target), with off-by-one fixes.
! ============================================================================
MODULE lulesh_comp_kernels

! Use Open-MP, the call on the module level covers all subroutines contained herein
#if _OPENMP
  USE OMP_LIB
#endif

IMPLICIT NONE
PRIVATE 

  !--------------------------------------------------------------
  ! Definition of data structure
  !--------------------------------------------------------------
  TYPE domain_type
  
    !--------------------------------------------------------------
    ! Node-centered 
    !--------------------------------------------------------------
  
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_x   ! coordinates - m_coord[idx].x 
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_y 
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_z 
  
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_xd  ! velocities - m_vel[idx].x
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_yd 
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_zd 
  
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_xdd  ! accelerations - m_acc[idx].x
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_ydd 
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_zdd 
  
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_fx   ! forces - m_force[idx].x
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_fy 
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_fz 
  
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_nodalMass   ! mass - m_nodalMass[idx]
  
    INTEGER,     DIMENSION(:), ALLOCATABLE ::m_symmX   ! symmetry plane nodesets - m_symmX[idx]
    INTEGER,     DIMENSION(:), ALLOCATABLE ::m_symmY 
    INTEGER,     DIMENSION(:), ALLOCATABLE ::m_symmZ
    LOGICAL ::m_symm_is_set

    ! Missing region information
    INTEGER,     DIMENSIOn(:), ALLOCATABLE ::m_regElemSize   ! Size of region sets
    INTEGER,     DIMENSION(:), ALLOCATABLE ::m_regNumList    ! Region number per domain element
    INTEGER,     DIMENSION(:), ALLOCATABLE ::m_regElemlist   ! Region indexset
    INTEGER,     DIMENSION(:), ALLOCATABLE ::m_regElemKeys   ! Keys to the slices of the region indexset
  
    INTEGER,     DIMENSION(:), ALLOCATABLE ::m_nodeElemCount 
    INTEGER,     DIMENSION(:), ALLOCATABLE ::m_nodeElemStart 
    !   INTEGER,     DIMENSION(:), ALLOCATABLE ::m_nodeElemList 
    INTEGER,     DIMENSION(:), ALLOCATABLE ::m_nodeElemCornerList
  
    ! Element-centered 
  
    INTEGER,     DIMENSION(:), ALLOCATABLE :: m_matElemlist   ! material indexset 
    INTEGER,     DIMENSION(:,:), POINTER   :: m_nodelist  ! elemToNode connectivity 
  
    INTEGER,     DIMENSION(:), ALLOCATABLE :: m_lxim   ! element connectivity across each face 
    INTEGER,     DIMENSION(:), ALLOCATABLE :: m_lxip 
    INTEGER,     DIMENSION(:), ALLOCATABLE :: m_letam 
    INTEGER,     DIMENSION(:), ALLOCATABLE :: m_letap 
    INTEGER,     DIMENSION(:), ALLOCATABLE :: m_lzetam 
    INTEGER,     DIMENSION(:), ALLOCATABLE :: m_lzetap 
  
    INTEGER,     DIMENSION(:), ALLOCATABLE :: m_elemBC   ! symmetry/free-surface flags for each elem face 
  
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_dxx   ! principal strains -- temporary 
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_dyy 
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_dzz 
  
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_delv_xi     ! velocity gradient -- temporary 
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_delv_eta 
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_delv_zeta 
  
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_delx_xi     ! coordinate gradient -- temporary 
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_delx_eta 
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_delx_zeta 
  
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_e    ! energy 
  
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_p    ! pressure 
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_q    ! q 
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_ql   ! linear term for q 
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_qq   ! quadratic term for q 
  
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_v      ! relative volume 
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_volo   ! reference volume 
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_vnew   ! new relative volume -- temporary 
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_delv   ! m_vnew - m_v 
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_vdov   ! volume derivative over volume 
  
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_arealg   ! characteristic length of an element 
  
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_ss       ! "sound speed" 
  
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: m_elemMass   ! mass 
  
    !--------------------------------------------------------------
    ! Parameters 
    !--------------------------------------------------------------
  
    REAL(KIND=8)       ::   m_dtfixed            ! fixed time increment 
    REAL(KIND=8)       ::   m_time               ! current time 
    REAL(KIND=8)       ::   m_deltatime          ! variable time increment 
    REAL(KIND=8)       ::   m_deltatimemultlb 
    REAL(KIND=8)       ::   m_deltatimemultub 
    REAL(KIND=8)       ::   m_stoptime           ! end time for simulation 
  
    REAL(KIND=8)       ::   m_u_cut              ! velocity tolerance 
    REAL(KIND=8)       ::   m_hgcoef             ! hourglass control 
    REAL(KIND=8)       ::   m_qstop              ! excessive q indicator 
    REAL(KIND=8)       ::   m_monoq_max_slope 
    REAL(KIND=8)       ::   m_monoq_limiter_mult 
    REAL(KIND=8)       ::   m_e_cut              ! energy tolerance 
    REAL(KIND=8)       ::   m_p_cut              ! pressure tolerance 
    REAL(KIND=8)       ::   m_ss4o3 
    REAL(KIND=8)       ::   m_q_cut              ! q tolerance 
    REAL(KIND=8)       ::   m_v_cut              ! relative volume tolerance 
    REAL(KIND=8)       ::   m_qlc_monoq          ! linear term coef for q 
    REAL(KIND=8)       ::   m_qqc_monoq          ! quadratic term coef for q 
    REAL(KIND=8)       ::   m_qqc 
    REAL(KIND=8)       ::   m_eosvmax 
    REAL(KIND=8)       ::   m_eosvmin 
    REAL(KIND=8)       ::   m_pmin               ! pressure floor 
    REAL(KIND=8)       ::   m_emin               ! energy floor 
    REAL(KIND=8)       ::   m_dvovmax            ! maximum allowable volume change 
    REAL(KIND=8)       ::   m_refdens            ! reference density 
  
    REAL(KIND=8)       ::   m_dtcourant          ! courant constraint 
    REAL(KIND=8)       ::   m_dthydro            ! volume change constraint 
    REAL(KIND=8)       ::   m_dtmax              ! maximum allowable time increment 
  
    INTEGER ::   m_cycle              ! Iteration count for simulation

    INTEGER :: m_numReg              ! Number of regions
    INTEGER :: m_cost                ! Imbalance cost
  
    INTEGER ::   m_sizeX            ! X,Y,Z extent of this block 
    INTEGER ::   m_sizeY 
    INTEGER ::   m_sizeZ 
  
    INTEGER ::   m_numElem          ! Elements/Nodes in this domain 
    INTEGER ::   m_numNode 
  
  END TYPE domain_type


  ! Define the publicly available subroutines and Datastructures
  PUBLIC :: domain_type
  PUBLIC :: AllocateNodalPersistent
  PUBLIC :: AllocateElemPersistent
  PUBLIC :: AllocateGradients
  PUBLIC :: DeallocateGradients
  PUBLIC :: AllocateStrains
  PUBLIC :: DeallocateStrains
  PUBLIC :: AllocateNodesets
  PUBLIC :: AllocateNodeElemIndexes
  PUBLIC :: InitMeshDecomp
  PUBLIC :: TimeIncrement
  PUBLIC :: InitStressTermsForElems
  PUBLIC :: CalcElemShapeFunctionDerivatives
  PUBLIC :: SumElemFaceNormal
  PUBLIC :: CalcElemNodeNormals
  PUBLIC :: SumElemStressesToNodeForces
  PUBLIC :: IntegrateStressForElems
  PUBLIC :: CollectDomainNodesToElemNodes
  PUBLIC :: VoluDer
  PUBLIC :: CalcElemVolumeDerivative
  PUBLIC :: CalcElemFBHourglassForce
  PUBLIC :: CalcFBHourglassForceForElems
  PUBLIC :: CalcHourglassControlForElems
  PUBLIC :: CalcVolumeForceForElems
  PUBLIC :: CalcForceForNodes
  PUBLIC :: CalcAccelerationForNodes
  PUBLIC :: ApplyAccelerationBoundaryConditionsForNodes
  PUBLIC :: CalcVelocityForNodes
  PUBLIC :: CalcPositionForNodes
  PUBLIC :: LagrangeNodal
  PUBLIC :: CalcElemCharacteristicLength
  PUBLIC :: CalcElemVelocityGrandient
  PUBLIC :: CalcKinematicsForElems
  PUBLIC :: CalcLagrangeElements
  PUBLIC :: CalcMonotonicQGradientsForElems
  PUBLIC :: CalcMonotonicQRegionForElems
  PUBLIC :: CalcMonotonicQForElems
  PUBLIC :: CalcQForElems
  PUBLIC :: CalcPressureForElems
  PUBLIC :: CalcEnergyForElems
  PUBLIC :: CalcSoundSpeedForElems
  PUBLIC :: EvalEOSForElems
  PUBLIC :: ApplyMaterialPropertiesForElems
  PUBLIC :: UpdateVolumesForElems
  PUBLIC :: LagrangeElements
  PUBLIC :: CalcCourantConstraintForElems
  PUBLIC :: CalcHydroConstraintForElems
  PUBLIC :: CalcTimeConstraintsForElems
  PUBLIC :: LagrangeLeapFrog
  PUBLIC :: luabort

  ! Define the publicly available functions
  PUBLIC :: CBRT
  PUBLIC :: CalcElemVolume
  PUBLIC :: TRIPLE_PRODUCT
  PUBLIC :: AreaFace

CONTAINS

  SUBROUTINE AllocateNodalPersistent(domain, numNode)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    INTEGER :: numNode
    INTEGER(KIND=4), PARAMETER :: RLK = 8

    ! Coordinates
    ALLOCATE(domain%m_x(0:numNode-1))
    ALLOCATE(domain%m_y(0:numNode-1)) 
    ALLOCATE(domain%m_z(0:numNode-1)) 

    ! Velocities
    ALLOCATE(domain%m_xd(0:numNode-1)) 
    ALLOCATE(domain%m_yd(0:numNode-1)) 
    ALLOCATE(domain%m_zd(0:numNode-1)) 
    
    ! Acceleration
    ALLOCATE(domain%m_xdd(0:numNode-1)) 
    ALLOCATE(domain%m_ydd(0:numNode-1)) 
    ALLOCATE(domain%m_zdd(0:numNode-1)) 

    ! Forces
    ALLOCATE(domain%m_fx(0:numNode-1)) 
    ALLOCATE(domain%m_fy(0:numNode-1)) 
    ALLOCATE(domain%m_fz(0:numNode-1)) 

    ALLOCATE(domain%m_nodalMass(0:numNode-1))

    !domain%m_xd = 0.0_RLK  ! Is this right? Not there in the C++ version
    !domain%m_yd = 0.0_RLK  ! Is this right? Not there in the C++ version
    !domain%m_zd = 0.0_RLK  ! Is this right? Not there in the C++ version
    !domain%m_xdd = 0.0_RLK  ! Is this right? Not there in the C++ version
    !domain%m_ydd = 0.0_RLK  ! Is this right? Not there in the C++ version
    !domain%m_zdd = 0.0_RLK  ! Is this right? Not there in the C++ version
    !domain%m_nodalMass = 0.0_RLK  ! Is this right? Not there in the C++ version

  END SUBROUTINE AllocateNodalPersistent


  SUBROUTINE AllocateElemPersistent(domain, numElem)
    IMPLICIT NONE
   
    TYPE(domain_type), INTENT(INOUT) :: domain
    INTEGER :: numElem
    INTEGER(KIND=4), PARAMETER :: RLK = 8

    ALLOCATE(domain%m_matElemlist(0:numElem-1))   ! What do I need matElemlist for?
    ALLOCATE(domain%m_nodelist(0:numElem-1, 0:7))

    ALLOCATE(domain%m_lxim(0:numElem-1)) 
    ALLOCATE(domain%m_lxip(0:numElem-1)) 
    ALLOCATE(domain%m_letam(0:numElem-1)) 
    ALLOCATE(domain%m_letap(0:numElem-1)) 
    ALLOCATE(domain%m_lzetam(0:numElem-1)) 
    ALLOCATE(domain%m_lzetap(0:numElem-1)) 

    ALLOCATE(domain%m_elemBC(0:numElem-1)) 

    ALLOCATE(domain%m_e(0:numElem-1)) 
    ALLOCATE(domain%m_p(0:numElem-1)) 
    ALLOCATE(domain%m_q(0:numElem-1)) 
    ALLOCATE(domain%m_ql(0:numElem-1)) 
    ALLOCATE(domain%m_qq(0:numElem-1)) 

    ALLOCATE(domain%m_v(0:numElem-1)) 
    ALLOCATE(domain%m_volo(0:numElem-1)) 
    ALLOCATE(domain%m_delv(0:numElem-1)) 
    ALLOCATE(domain%m_vdov(0:numElem-1)) 

    ALLOCATE(domain%m_arealg(0:numElem-1)) 
    ALLOCATE(domain%m_ss(0:numElem-1))
    ALLOCATE(domain%m_elemMass(0:numElem-1))

    ALLOCATE(domain%m_vnew(0:numElem-1))

    !domain%m_e = 0.0_RLK  ! Is this correct here? Does not appear in C++ code.
    !domain%m_v = 1.0_RLK  ! Is this correct here? Does not appear in C++ code.
    !domain%m_p = 0.0_RLK  ! Is this correct here? Does not appear in C++ code.

  END SUBROUTINE AllocateElemPersistent


  SUBROUTINE AllocateGradients(domain, numElem, allElem)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    INTEGER :: numElem, allElem

    ! Position gradients
    ALLOCATE(domain%m_delx_xi(0:numElem-1))
    ALLOCATE(domain%m_delx_eta(0:numElem-1))
    ALLOCATE(domain%m_delx_zeta(0:numElem-1))

    ! Velocity gradients
    ALLOCATE(domain%m_delv_xi(0:allElem-1))
    ALLOCATE(domain%m_delv_eta(0:allElem-1))
    ALLOCATE(domain%m_delv_zeta(0:allElem-1))

  END SUBROUTINE AllocateGradients


  SUBROUTINE DeallocateGradients(domain)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain

    DEALLOCATE(domain%m_delx_zeta)
    DEALLOCATE(domain%m_delx_eta)
    DEALLOCATE(domain%m_delx_xi)

    DEALLOCATE(domain%m_delv_zeta)
    DEALLOCATE(domain%m_delv_eta)
    DEALLOCATE(domain%m_delv_xi)

  END SUBROUTINE DeallocateGradients


  SUBROUTINE AllocateStrains(domain, numElem)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    INTEGER :: numElem

    ! Strain
    ALLOCATE(domain%m_dxx(0:numElem-1))
    ALLOCATE(domain%m_dyy(0:numElem-1))
    ALLOCATE(domain%m_dzz(0:numElem-1))

  END SUBROUTINE


  SUBROUTINE DeallocateStrains(domain)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain

    DEALLOCATE(domain%m_dzz)
    DEALLOCATE(domain%m_dyy)
    DEALLOCATE(domain%m_dxx)

  END SUBROUTINE DeallocateStrains


  ! Really unsure about this one here!
  SUBROUTINE AllocateNodesets(domain, edgeNodes_sq)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    INTEGER :: edgeNodes_sq

    ALLOCATE(domain%m_symmX(0:edgeNodes_sq-1))
    ALLOCATE(domain%m_symmY(0:edgeNodes_sq-1))
    ALLOCATE(domain%m_symmZ(0:edgeNodes_sq-1))

  END SUBROUTINE AllocateNodesets


  ! Really unsure about this one here!
  SUBROUTINE AllocateNodeElemIndexes(domain)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    INTEGER :: numElem
    INTEGER :: m
    INTEGER :: i,j,k
    INTEGER :: offset
    INTEGER :: clSize, clv
    INTEGER :: numNode
    INTEGER :: numCorner

    numElem = domain%m_numElem
    numNode = domain%m_numNode

    PRINT *, 'Number of elements: ', numElem
    PRINT *, 'Number of nodes: ', numNode

    ! set up node-centered indexing of elements (canonical LULESH).
    ! m_nodelist has shape (0:numElem-1, 0:7): row = element, col = local corner.
    ALLOCATE(domain%m_nodeElemCount(0:numNode-1))
    domain%m_nodeElemCount=0

    DO i=0, numElem-1
       DO j=0, 7
          m = domain%m_nodelist(i, j)
          domain%m_nodeElemCount(m) = domain%m_nodeElemCount(m) + 1
       END DO
    END DO

    ALLOCATE(domain%m_nodeElemStart(0:numNode-1))
    domain%m_nodeElemStart=0

    DO i=1,numNode-1
       domain%m_nodeElemStart(i) = domain%m_nodeElemStart(i-1) + domain%m_nodeElemCount(i-1)
    END DO

    ! Corner list is 1-D, length = total corners = numElem*8.  It stores
    ! per-node the flat corner index k = elem*8 + localCorner.
    numCorner = domain%m_nodeElemStart(numNode-1) + domain%m_nodeElemCount(numNode-1)
    ALLOCATE(domain%m_nodeElemCornerList(0:numCorner-1))

    domain%m_nodeElemCount=0

    DO i=0, numElem-1
       DO j=0, 7
          m = domain%m_nodelist(i, j)
          k = i*8 + j
          offset = domain%m_nodeElemStart(m) + domain%m_nodeElemCount(m)
          domain%m_nodeElemCornerList(offset) = k
          domain%m_nodeElemCount(m) = domain%m_nodeElemCount(m) + 1
       END DO
    END DO

    ! Double-check that the corner indices are not out of bounds
    clSize = SIZE(domain%m_nodeElemCornerList)
    DO i=0, clSize-1
       clv=domain%m_nodeElemCornerList(i)
       IF ((clv.LT.0).OR.(clv.GT.numElem*8))THEN
          PRINT*,"AllocateNodeElemIndexes(): nodeElemCornerList entry out of range!"
          CALL luabort(1)
       END IF
    END DO


  END SUBROUTINE AllocateNodeElemIndexes


  SUBROUTINE InitMeshDecomp(numRanks, myRank, col, row, plane, side)
    IMPLICIT NONE

    INTEGER, INTENT(IN) :: numRanks
    INTEGER, INTENT(IN) :: myRank
    INTEGER, INTENT(OUT) :: col
    INTEGER, INTENT(OUT) :: row
    INTEGER, INTENT(OUT) :: plane
    INTEGER, INTENT(OUT) :: side

    INTEGER(KIND=4) :: testProcs
    INTEGER(KIND=4) :: dx, dy, dz
    INTEGER(KIND=4) :: myDom
    INTEGER(KIND=4) :: remainder
    INTEGER(KIND=4), PARAMETER :: RLK = 8

    ! Assume cube processor layout for now
    testProcs = NINT((numRanks**(1.0_RLK/3.0_RLK))+0.5_RLK)
    IF (testProcs*testProcs*testProcs /= numRanks) THEN
      STOP
    ENDIF

    dx = testProcs
    dy = testProcs
    dz = testProcs

    remainder = MOD(dx*dy*dz, numRanks)
    IF (myRank < remainder) THEN
      myDom = myRank*(1 + (dx*dy*dz / numRanks))
    ELSE
      myDom = remainder*(1 + (dx*dy*dz / numRanks)) + &
          (myRank - remainder)*(dx*dy*dz / numRanks)
    ENDIF

    col   = MOD(myDom, testProcs)
    row   = MOD(myDom / testProcs, testProcs)
    plane = myDom / (testProcs*testProcs)
    side  = testProcs

  END SUBROUTINE



  SUBROUTINE TimeIncrement(domain)
    IMPLICIT NONE 

    TYPE(domain_type), INTENT(INOUT) :: domain
    REAL(KIND=8) :: targetdt
    REAL(KIND=8) :: ratio, olddt, newdt, gnewdt
    INTEGER(KIND=4), PARAMETER :: RLK = 8

    targetdt = domain%m_stoptime - domain%m_time

    IF (( domain%m_dtfixed <= 0.0_RLK) .AND. (domain%m_cycle /= 0)) THEN

       olddt = domain%m_deltatime

       ! This will require a reduction in parallel
       gnewdt = 1.0e+20

       IF (domain%m_dtcourant < gnewdt) THEN
          gnewdt = domain%m_dtcourant / 2.0_RLK
       END IF

       IF (domain%m_dthydro < gnewdt) THEN
          gnewdt = domain%m_dthydro * (2.0_RLK/3.0_RLK)
       END IF

       newdt = gnewdt
       ratio = newdt / olddt

       IF (ratio >= 1.0_RLK) THEN
          IF (ratio < domain%m_deltatimemultlb) THEN
             newdt = olddt
          ELSE IF (ratio > domain%m_deltatimemultub) THEN
             newdt = olddt*domain%m_deltatimemultub
          END IF
       END IF

       IF (newdt > domain%m_dtmax) THEN
          newdt = domain%m_dtmax
       END IF
       domain%m_deltatime = newdt

    END IF

    ! TRY TO PREVENT VERY SMALL SCALING ON THE NEXT CYCLE
    IF ((targetdt > domain%m_deltatime) .AND. (targetdt < (4.0_RLK * domain%m_deltatime / 3.0_RLK))) THEN
       targetdt = 2.0_RLK * domain%m_deltatime / 3.0_RLK
    END IF

    IF (targetdt < domain%m_deltatime) THEN
       domain%m_deltatime = targetdt
    END IF

    domain%m_time = domain%m_time+domain%m_deltatime
    domain%m_cycle = domain%m_cycle+1

  END SUBROUTINE TimeIncrement



  SUBROUTINE InitStressTermsForElems(domain, sigxx, sigyy, sigzz, numElem)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    INTEGER         :: numElem
    REAL(KIND=8), DIMENSION(0:) :: sigxx  ! Is the dimension correct here?
    REAL(KIND=8), DIMENSION(0:) :: sigyy
    REAL(KIND=8), DIMENSION(0:) :: sigzz
    INTEGER(KIND=4) :: ii

!$OMP PARALLEL DO PRIVATE(ii) DEFAULT(none) SHARED(domain, sigxx, sigyy, sigzz)
    DO ii = 1, numElem
      sigxx(ii) =  - domain%m_p(ii) - domain%m_q(ii)
      sigyy(ii) =  - domain%m_p(ii) - domain%m_q(ii)
      sigzz(ii) =  - domain%m_p(ii) - domain%m_q(ii)
    ENDDO 

 END SUBROUTINE InitStressTermsForElems



  SUBROUTINE CalcElemShapeFunctionDerivatives( x, y, z,   &
                                               b,         &
                                               el_volume   )
    IMPLICIT NONE 

    REAL(KIND=8), DIMENSION(0:7)  :: x, y, z
    REAL(KIND=8), DIMENSION(0:7,0:2) :: b
    REAL(KIND=8), INTENT(INOUT) :: el_volume
    INTEGER(KIND=4), PARAMETER :: RLK = 8
    REAL(KIND=8)  :: x0, x1, x2, x3, x4, x5, x6, x7
    REAL(KIND=8)  :: y0, y1, y2, y3, y4, y5, y6, y7
    REAL(KIND=8)  :: z0, z1, z2, z3, z4, z5, z6, z7

    REAL(KIND=8)  :: fjxxi, fjxet, fjxze
    REAL(KIND=8)  :: fjyxi, fjyet, fjyze
    REAL(KIND=8)  :: fjzxi, fjzet, fjzze
    REAL(KIND=8)  :: cjxxi, cjxet, cjxze
    REAL(KIND=8)  :: cjyxi, cjyet, cjyze
    REAL(KIND=8)  :: cjzxi, cjzet, cjzze

    x0 = x(0)
    x1 = x(1)
    x2 = x(2)
    x3 = x(3)
    x4 = x(4)
    x5 = x(5)
    x6 = x(6)
    x7 = x(7)

    y0 = y(0)
    y1 = y(1)
    y2 = y(2)
    y3 = y(3)
    y4 = y(4)
    y5 = y(5)
    y6 = y(6)
    y7 = y(7)

    z0 = z(0)
    z1 = z(1)
    z2 = z(2)
    z3 = z(3)
    z4 = z(4)
    z5 = z(5)
    z6 = z(6)
    z7 = z(7)

    fjxxi = .125_RLK * ( (x6-x0) + (x5-x3) - (x7-x1) - (x4-x2) )
    fjxet = .125_RLK * ( (x6-x0) - (x5-x3) + (x7-x1) - (x4-x2) )
    fjxze = .125_RLK * ( (x6-x0) + (x5-x3) + (x7-x1) + (x4-x2) )

    fjyxi = .125_RLK * ( (y6-y0) + (y5-y3) - (y7-y1) - (y4-y2) )
    fjyet = .125_RLK * ( (y6-y0) - (y5-y3) + (y7-y1) - (y4-y2) )
    fjyze = .125_RLK * ( (y6-y0) + (y5-y3) + (y7-y1) + (y4-y2) )

    fjzxi = .125_RLK * ( (z6-z0) + (z5-z3) - (z7-z1) - (z4-z2) )
    fjzet = .125_RLK * ( (z6-z0) - (z5-z3) + (z7-z1) - (z4-z2) )
    fjzze = .125_RLK * ( (z6-z0) + (z5-z3) + (z7-z1) + (z4-z2) )

    ! Compute cofactors
    cjxxi =    (fjyet * fjzze) - (fjzet * fjyze)
    cjxet =  - (fjyxi * fjzze) + (fjzxi * fjyze)
    cjxze =    (fjyxi * fjzet) - (fjzxi * fjyet)

    cjyxi =  - (fjxet * fjzze) + (fjzet * fjxze)
    cjyet =    (fjxxi * fjzze) - (fjzxi * fjxze)
    cjyze =  - (fjxxi * fjzet) + (fjzxi * fjxet)

    cjzxi =    (fjxet * fjyze) - (fjyet * fjxze)
    cjzet =  - (fjxxi * fjyze) + (fjyxi * fjxze)
    cjzze =    (fjxxi * fjyet) - (fjyxi * fjxet)

    ! calculate partials :
    !     this need only be done for l = 0,1,2,3   since , by symmetry ,
    !     (6,7,4,5) = - (0,1,2,3) .
    b(0,0) =   -  cjxxi  -  cjxet  -  cjxze
    b(1,0) =      cjxxi  -  cjxet  -  cjxze
    b(2,0) =      cjxxi  +  cjxet  -  cjxze
    b(3,0) =   -  cjxxi  +  cjxet  -  cjxze
    b(4,0) = -b(2,0)
    b(5,0) = -b(3,0)
    b(6,0) = -b(0,0)
    b(7,0) = -b(1,0)

    b(0,1) =   -  cjyxi  -  cjyet  -  cjyze
    b(1,1) =      cjyxi  -  cjyet  -  cjyze
    b(2,1) =      cjyxi  +  cjyet  -  cjyze
    b(3,1) =   -  cjyxi  +  cjyet  -  cjyze
    b(4,1) = -b(2,1)
    b(5,1) = -b(3,1)
    b(6,1) = -b(0,1)
    b(7,1) = -b(1,1)

    b(0,2) =   -  cjzxi  -  cjzet  -  cjzze
    b(1,2) =      cjzxi  -  cjzet  -  cjzze
    b(2,2) =      cjzxi  +  cjzet  -  cjzze
    b(3,2) =   -  cjzxi  +  cjzet  -  cjzze
    b(4,2) = -b(2,2)
    b(5,2) = -b(3,2)  ! Indices adjusted
    b(6,2) = -b(0,2)  ! Indices adjusted
    b(7,2) = -b(1,2)  ! Indices adjusted

    ! Calculate jacobian determinant (volume)
    el_volume = 8.0_RLK * ( fjxet * cjxet + fjyet * cjyet + fjzet * cjzet)

  END SUBROUTINE CalcElemShapeFunctionDerivatives



  SUBROUTINE SumElemFaceNormal(normalX0, normalY0, normalZ0, &
                               normalX1, normalY1, normalZ1, &
                               normalX2, normalY2, normalZ2, &
                               normalX3, normalY3, normalZ3, &
                                x0,  y0,  z0,    &
                                x1,  y1,  z1,    &
                                x2,  y2,  z2,    &
                                x3,  y3,  z3     )
    IMPLICIT NONE

    ! The normals here should be pointer!
    REAL(KIND=8) :: normalX0, normalY0, normalZ0
    REAL(KIND=8) :: normalX1, normalY1, normalZ1
    REAL(KIND=8) :: normalX2, normalY2, normalZ2
    REAL(KIND=8) :: normalX3, normalY3, normalZ3
    REAL(KIND=8) :: x0, y0, z0
    REAL(KIND=8) :: x1, y1, z1
    REAL(KIND=8) :: x2, y2, z2
    REAL(KIND=8) :: x3, y3, z3
  
    REAL(KIND=8) :: bisectX0
    REAL(KIND=8) :: bisectY0
    REAL(KIND=8) :: bisectZ0
    REAL(KIND=8) :: bisectX1
    REAL(KIND=8) :: bisectY1
    REAL(KIND=8) :: bisectZ1
    REAL(KIND=8) :: areaX
    REAL(KIND=8) :: areaY
    REAL(KIND=8) :: areaZ
    INTEGER(KIND=4), PARAMETER :: RLK = 8

    bisectX0 = 0.5_RLK * (x3 + x2 - x1 - x0)
    bisectY0 = 0.5_RLK * (y3 + y2 - y1 - y0)
    bisectZ0 = 0.5_RLK * (z3 + z2 - z1 - z0)
    bisectX1 = 0.5_RLK * (x2 + x1 - x3 - x0)
    bisectY1 = 0.5_RLK * (y2 + y1 - y3 - y0)
    bisectZ1 = 0.5_RLK * (z2 + z1 - z3 - z0)
    areaX = 0.25_RLK * (bisectY0 * bisectZ1 - bisectZ0 * bisectY1)
    areaY = 0.25_RLK * (bisectZ0 * bisectX1 - bisectX0 * bisectZ1)
    areaZ = 0.25_RLK * (bisectX0 * bisectY1 - bisectY0 * bisectX1)

    normalX0 = normalX0 + areaX
    normalX1 = normalX1 + areaX
    normalX2 = normalX2 + areaX
    normalX3 = normalX3 + areaX

    normalY0 = normalY0 + areaY
    normalY1 = normalY1 + areaY
    normalY2 = normalY2 + areaY
    normalY3 = normalY3 + areaY

    normalZ0 = normalZ0 + areaZ
    normalZ1 = normalZ1 + areaZ
    normalZ2 = normalZ2 + areaZ
    normalZ3 = normalZ3 + areaZ

  END SUBROUTINE SumElemFaceNormal


  SUBROUTINE CalcElemNodeNormals(pfx,pfy, pfz, x, y, z)
    IMPLICIT NONE

    REAL(KIND=8), DIMENSION(0:7) :: pfx,pfy,pfz
    REAL(KIND=8), DIMENSION(0:7) :: x, y, z
    INTEGER(KIND=4), PARAMETER :: RLK = 8
    INTEGER(KIND=4) :: i

    DO i = 0, 7
      pfx(i) = 0.0_RLK
      pfy(i) = 0.0_RLK
      pfz(i) = 0.0_RLK
    ENDDO

    ! Evaluate face one: nodes 0, 1, 2, 3
    CALL SumElemFaceNormal(pfx(0), pfy(0), pfz(0),              &
                           pfx(1), pfy(1), pfz(1),              &
                           pfx(2), pfy(2), pfz(2),              &
                           pfx(3), pfy(3), pfz(3),              &
                           x(0), y(0), z(0), x(1), y(1), z(1),  &
                           x(2), y(2), z(2), x(3), y(3), z(3))
    ! Evaluate face two: nodes 0, 4, 5, 1
    CALL SumElemFaceNormal(pfx(0), pfy(0), pfz(0),              &
                           pfx(4), pfy(4), pfz(4),              &
                           pfx(5), pfy(5), pfz(5),              &
                           pfx(1), pfy(1), pfz(1),              &
                           x(0), y(0), z(0), x(4), y(4), z(4),  &
                           x(5), y(5), z(5), x(1), y(1), z(1))
    ! Evaluate face three: nodes 1, 5, 6, 2
    CALL SumElemFaceNormal(pfx(1), pfy(1), pfz(1),              &
                           pfx(5), pfy(5), pfz(5),              &
                           pfx(6), pfy(6), pfz(6),              &
                           pfx(2), pfy(2), pfz(2),              &
                           x(1), y(1), z(1), x(5), y(5), z(5),  &
                           x(6), y(6), z(6), x(2), y(2), z(2))
    ! Evaluate face four: nodes 2, 6, 7, 3
    CALL SumElemFaceNormal(pfx(2), pfy(2), pfz(2),              &
                           pfx(6), pfy(6), pfz(6),              &
                           pfx(7), pfy(7), pfz(7),              &
                           pfx(3), pfy(3), pfz(3),              &
                           x(2), y(2), z(2), x(6), y(6), z(6),  &
                           x(7), y(7), z(7), x(3), y(3), z(3))
    ! Evaluate face five: nodes 3, 7, 4, 0
    CALL SumElemFaceNormal(pfx(3), pfy(3), pfz(3),              &
                           pfx(7), pfy(7), pfz(7),              &
                           pfx(4), pfy(4), pfz(4),              &
                           pfx(0), pfy(0), pfz(0),              &
                           x(3), y(3), z(3), x(7), y(7), z(7),  &
                           x(4), y(4), z(4), x(0), y(0), z(0))
    ! Evaluate face six: nodes 4, 7, 6, 5
    CALL SumElemFaceNormal(pfx(4), pfy(4), pfz(4),              &
                           pfx(7), pfy(7), pfz(7),              &
                           pfx(6), pfy(6), pfz(6),              &
                           pfx(5), pfy(5), pfz(5),              &
                           x(4), y(4), z(4), x(7), y(7), z(7),  &
                           x(6), y(6), z(6), x(5), y(5), z(5))

  END SUBROUTINE CalcElemNodeNormals



  SUBROUTINE SumElemStressesToNodeForces(B, stress_xx, stress_yy, stress_zz, &
                                         fx,  fy,  fz)
    IMPLICIT NONE

    REAL(KIND=8) ,DIMENSION(0:7, 0:2) :: B
    REAL(KIND=8) :: stress_xx, stress_yy, stress_zz
    REAL(KIND=8), DIMENSION(0:7) ::  fx,  fy,  fz
    INTEGER(KIND=4) :: i

    DO i=0, 7
      fx(i) = - ( stress_xx * B(i, 0) )
      fy(i) = - ( stress_yy * B(i, 1) )
      fz(i) = - ( stress_zz * B(i, 2) )
    END DO

  END SUBROUTINE SumElemStressesToNodeForces



  SUBROUTINE IntegrateStressForElems(domain, sigxx, sigyy, sigzz, determ, numElem)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    INTEGER      :: numElem
    REAL(KIND=8), DIMENSION(0:) :: sigxx, sigyy, sigzz
    REAL(KIND=8), DIMENSION(0:), INTENT(INOUT) :: determ
    INTEGER(KIND=4), PARAMETER :: RLK = 8

    REAL(KIND=8) :: fx_tmp, fy_tmp, fz_tmp
    REAL(KIND=8), DIMENSION(:), ALLOCATABLE :: fx_local, fy_local, fz_local
    REAL(KIND=8), DIMENSION(:), ALLOCATABLE :: fx_elem, fy_elem, fz_elem
    REAL(KIND=8), DIMENSION(0:7,0:2) :: B   ! shape function derivatives
    REAL(KIND=8), DIMENSION(0:7)   :: x_local
    REAL(KIND=8), DIMENSION(0:7)   :: y_local
    REAL(KIND=8), DIMENSION(0:7)   :: z_local
    INTEGER(KIND=4), DIMENSION(:), POINTER :: elemToNode
    INTEGER      :: lnode, gnode, count, ielem, kk
    INTEGER      :: numNode
    INTEGER      :: offset
    INTEGER, DIMENSION(:), POINTER :: cornerList
    INTEGER      :: numElem8
    INTEGER(KIND=4) :: i
    INTEGER(KIND=4) :: numthreads

    numNode = domain%m_numNode
    numElem8 = numElem * 8

#if _OPENMP
      numthreads = OMP_GET_MAX_THREADS()
#else
      numthreads = 1
#endif

    IF (numthreads  > 1) THEN
      ! Is this right? - Unsure about the allocation
      ALLOCATE(fx_elem(0:numElem8-1))
      ALLOCATE(fy_elem(0:numElem8-1))
      ALLOCATE(fz_elem(0:numElem8-1))
    ENDIF
    
!$OMP PARALLEL DO PRIVATE(kk, lnode, gnode, elemToNode, B, x_local, y_local, z_local)  &
!$OMP DEFAULT(none) SHARED(domain, sigxx, sigyy, sigzz, fx_elem, fy_elem, fz_elem)
    DO kk=0, numElem-1
      elemToNode => domain%m_nodelist(kk, :)  ! Adjusted index here

      ! Get nodal coordinates from global arrays and copy into local arrays.
      Call CollectDomainNodesToElemNodes(domain, elemToNode, x_local, y_local, z_local)

      ! Volume calculation involves extra work for numerical consistency.
      CALL CalcElemShapeFunctionDerivatives(x_local, y_local, z_local, &
                                            B, determ(kk))

      CALL CalcElemNodeNormals(B(:,0) , B(:,1), B(:,2), x_local, y_local, z_local)

      IF ( numthreads > 1) THEN
        ! Eliminate thread writing conflicts at the nodes by giving
        ! each element its own copy of the data.
        CALL SumElemStressesToNodeForces(B, sigxx(kk), sigyy(kk), &
                                         sigzz(kk), fx_elem,      &
                                         fy_elem, fz_elem)
      ELSE
        CALL SumElemStressesToNodeForces(B, sigxx(kk), sigyy(kk), &
                                         sigzz(kk), fx_local,     &
                                         fy_local, fz_local)

        ! Copy nodal force contributions to global force array
        DO lnode=0, 7
          gnode = elemToNode(lnode)  ! Needs to be exclusive to each thread, otherwise -> race-condition
          domain%m_fx(gnode) = domain%m_fx(gnode) + fx_local(lnode)
          domain%m_fy(gnode) = domain%m_fy(gnode) + fy_local(lnode)
          domain%m_fz(gnode) = domain%m_fz(gnode) + fz_local(lnode)
        END DO
      ENDIF
    ENDDO


    IF (numthreads > 1) THEN
!$OMP PARALLEL DO PRIVATE(gnode, count, cornerList, fx_tmp, fy_tmp, fz_tmp, i, ielem)  &
!$OMP DEFAULT(none) SHARED(domain, fx_elem, fy_elem, fz_elem)
      DO gnode=0, numNode-1
        count = domain%m_nodeElemCount(gnode)
        offset = domain%m_nodeElemStart(gnode)
        fx_tmp = 0.0_RLK
        fy_tmp = 0.0_RLK
        fz_tmp = 0.0_RLK
        DO i=0, count-1
          ielem = domain%m_nodeElemCornerList(offset+i)
          fx_tmp = fx_tmp + fx_elem(ielem)
          fy_tmp = fy_tmp + fy_elem(ielem)
          fz_tmp = fz_tmp + fz_elem(ielem)
        END DO
        domain%m_fx(gnode) = fx_tmp
        domain%m_fy(gnode) = fy_tmp
        domain%m_fz(gnode) = fz_tmp
      END DO

      ! Deallocate fx, fy, fz elems
      DEALLOCATE(fx_elem)
      DEALLOCATE(fy_elem)
      DEALLOCATE(fz_elem)
    ENDIF

  END SUBROUTINE IntegrateStressForElems



  SUBROUTINE CollectDomainNodesToElemNodes(domain, elemToNode, elemX, elemY, elemZ)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    INTEGER, DIMENSION(:), POINTER :: elemToNode
    REAL(KIND=8),DIMENSION(0:7), INTENT(INOUT)    :: elemX, elemY, elemZ

    INTEGER(KIND=4) :: nd0i, nd1i, nd2i, nd3i
    INTEGER(KIND=4) :: nd4i, nd5i, nd6i, nd7i

    nd0i = elemToNode(0)
    nd1i = elemToNode(1)
    nd2i = elemToNode(2)
    nd3i = elemToNode(3)
    nd4i = elemToNode(4)
    nd5i = elemToNode(5)
    nd6i = elemToNode(6)
    nd7i = elemToNode(7)

    elemX(0) = domain%m_x(nd0i)
    elemX(1) = domain%m_x(nd1i)
    elemX(2) = domain%m_x(nd2i)
    elemX(3) = domain%m_x(nd3i)
    elemX(4) = domain%m_x(nd4i)
    elemX(5) = domain%m_x(nd5i)
    elemX(6) = domain%m_x(nd6i)
    elemX(7) = domain%m_x(nd7i)

    elemY(0) = domain%m_y(nd0i)
    elemY(1) = domain%m_y(nd1i)
    elemY(2) = domain%m_y(nd2i)
    elemY(3) = domain%m_y(nd3i)
    elemY(4) = domain%m_y(nd4i)
    elemY(5) = domain%m_y(nd5i)
    elemY(6) = domain%m_y(nd6i)
    elemY(7) = domain%m_y(nd7i)

    elemZ(0) = domain%m_z(nd0i)
    elemZ(1) = domain%m_z(nd1i)
    elemZ(2) = domain%m_z(nd2i)
    elemZ(3) = domain%m_z(nd3i)
    elemZ(4) = domain%m_z(nd4i)
    elemZ(5) = domain%m_z(nd5i)
    elemZ(6) = domain%m_z(nd6i)
    elemZ(7) = domain%m_z(nd7i)

  END SUBROUTINE CollectDomainNodesToElemNodes



  SUBROUTINE VoluDer(x0, x1, x2,      &
                     x3, x4, x5,      &
                     y0, y1, y2,      &
                     y3, y4, y5,      &
                     z0, z1, z2,      &
                     z3, z4, z5,      &
                     dvdx, dvdy, dvdz )
    IMPLICIT NONE

    REAL(KIND=8) :: x0, x1, x2, x3, x4, x5
    REAL(KIND=8) :: y0, y1, y2, y3, y4, y5
    REAL(KIND=8) :: z0, z1, z2, z3, z4, z5
    REAL(KIND=8) :: dvdx, dvdy, dvdz
    INTEGER(KIND=4), PARAMETER :: RLK = 8

    REAL(KIND=8), PARAMETER :: twelfth = 1.0_RLK / 12.0_RLK

    dvdx =                                              &
      (y1 + y2) * (z0 + z1) - (y0 + y1) * (z1 + z2) +   &
      (y0 + y4) * (z3 + z4) - (y3 + y4) * (z0 + z4) -   &
      (y2 + y5) * (z3 + z5) + (y3 + y5) * (z2 + z5)
    dvdy =                                              &
      - (x1 + x2) * (z0 + z1) + (x0 + x1) * (z1 + z2) - &
      (x0 + x4) * (z3 + z4) + (x3 + x4) * (z0 + z4) +   &
      (x2 + x5) * (z3 + z5) - (x3 + x5) * (z2 + z5)
    dvdz =                                              &
      - (y1 + y2) * (x0 + x1) + (y0 + y1) * (x1 + x2) - &
      (y0 + y4) * (x3 + x4) + (y3 + y4) * (x0 + x4) +   &
      (y2 + y5) * (x3 + x5) - (y3 + y5) * (x2 + x5)

    dvdx = dvdx * twelfth
    dvdy = dvdy * twelfth
    dvdz = dvdz * twelfth

  END SUBROUTINE VoluDer



  SUBROUTINE CalcElemVolumeDerivative(dvdx, dvdy, dvdz, x, y, z)
    IMPLICIT NONE

    REAL(KIND=8),DIMENSION(0:7) :: dvdx, dvdy, dvdz
    REAL(KIND=8),DIMENSION(0:7) :: x, y, z

    CALL VoluDer(x(1), x(2), x(3), x(4), x(5), x(7),  &
                 y(1), y(2), y(3), y(4), y(5), y(7),  &
                 z(1), z(2), z(3), z(4), z(5), z(7),  &
                 dvdx(0), dvdy(0), dvdz(0))
    CALL VoluDer(x(0), x(1), x(2), x(7), x(4), x(6),  &
                 y(0), y(1), y(2), y(7), y(4), y(6),  &
                 z(0), z(1), z(2), z(7), z(4), z(6),  &
                 dvdx(3), dvdy(3), dvdz(3))
    CALL VoluDer(x(3), x(0), x(1), x(6), x(7), x(5),  &
                 y(3), y(0), y(1), y(6), y(7), y(5),  &
                 z(3), z(0), z(1), z(6), z(7), z(5),  &
                 dvdx(2), dvdy(2), dvdz(2))
    CALL VoluDer(x(2), x(3), x(0), x(5), x(6), x(4),  &
                 y(2), y(3), y(0), y(5), y(6), y(4),  &
                 z(2), z(3), z(0), z(5), z(6), z(4),  &
                 dvdx(1), dvdy(1), dvdz(1))
    CALL VoluDer(x(7), x(6), x(5), x(0), x(3), x(1),  &
                 y(7), y(6), y(5), y(0), y(3), y(1),  &
                 z(7), z(6), z(5), z(0), z(3), z(1),  &
                 dvdx(4), dvdy(4), dvdz(4))
    CALL VoluDer(x(4), x(7), x(6), x(1), x(0), x(2),  &
                 y(4), y(7), y(6), y(1), y(0), y(2),  &
                 z(4), z(7), z(6), z(1), z(0), z(2),  &
                 dvdx(5), dvdy(5), dvdz(5))
    CALL VoluDer(x(5), x(4), x(7), x(2), x(1), x(3),  &
                 y(5), y(4), y(7), y(2), y(1), y(3),  &
                 z(5), z(4), z(7), z(2), z(1), z(3),  &
                 dvdx(6), dvdy(6), dvdz(6))
    CALL VoluDer(x(6), x(5), x(4), x(3), x(2), x(0),  &
                 y(6), y(5), y(4), y(3), y(2), y(0),  &
                 z(6), z(5), z(4), z(3), z(2), z(0),  &
                 dvdx(7), dvdy(7), dvdz(7))

  END SUBROUTINE CalcElemVolumeDerivative



  SUBROUTINE CalcElemFBHourglassForce(xd, yd, zd,         &
                                      hourgam,            &
                                      coefficient, hgfx,  &
                                      hgfy, hgfz          )
    IMPLICIT NONE
    REAL(KIND=8), DIMENSION(0:7) :: xd,yd,zd
    REAL(KIND=8), DIMENSION(0:3,0:7) :: hourgam
    REAL(KIND=8) :: coefficient
    REAL(KIND=8), DIMENSION(0:7) :: hgfx,hgfy,hgfz
    REAL(KIND=8), DIMENSION(0:3) :: hxx
    INTEGER(KIND=4) :: i

    DO i=0, 3
      hxx(i) = hourgam(i, 0) * xd(0) + hourgam(i, 1) * xd(1) + &
               hourgam(i, 2) * xd(2) + hourgam(i, 3) * xd(3) + &
               hourgam(i, 4) * xd(4) + hourgam(i, 5) * xd(5) + &
               hourgam(i, 6) * xd(6) + hourgam(i, 7) * xd(7)
    END DO

    DO i=0, 7
      hgfx(i) = coefficient * (hxx(0) * hourgam(0, i) + &
                               hxx(1) * hourgam(1, i) + &
                               hxx(2) * hourgam(2, i) + &
                               hxx(3) * hourgam(3, i))
    END DO

    DO i=0, 3
      hxx(i) = hourgam(i, 0) * yd(0) + hourgam(i, 1) * yd(1) + &
               hourgam(i, 2) * yd(2) + hourgam(i, 3) * yd(3) + &
               hourgam(i, 4) * yd(4) + hourgam(i, 5) * yd(5) + &
               hourgam(i, 6) * yd(6) + hourgam(i, 7) * yd(7)
    END DO

    DO i=0, 7
      hgfy(i) = coefficient * (hxx(0) * hourgam(0, i) + &
                               hxx(1) * hourgam(1, i) + &
                               hxx(2) * hourgam(2, i) + &
                               hxx(3) * hourgam(3, i))
    END DO

    DO i=0, 3
      hxx(i) = hourgam(i, 0) * zd(0) + hourgam(i, 1) * zd(1) + &
               hourgam(i, 2) * zd(2) + hourgam(i, 3) * zd(3) + &
               hourgam(i, 4) * zd(4) + hourgam(i, 5) * zd(5) + &
               hourgam(i, 6) * zd(6) + hourgam(i, 7) * zd(7)
    END DO

    DO i=0, 7
      hgfz(i) = coefficient * (hxx(0) * hourgam(0, i) + &
                               hxx(1) * hourgam(1, i) + &
                               hxx(2) * hourgam(2, i) + &
                               hxx(3) * hourgam(3, i))
    END DO

  END SUBROUTINE CalcElemFBHourglassForce



  SUBROUTINE CalcFBHourglassForceForElems(domain, determ,   &
                                          x8n, y8n, z8n,    &
                                          dvdx, dvdy, dvdz, &
                                          hourg             )

  ! *************************************************
  ! *
  ! *     FUNCTION: Calculates the Flanagan-Belytschko anti-hourglass
  ! *               force.
  ! *
  ! *************************************************
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    REAL(KIND=8), DIMENSION(0:) :: determ
    REAL(KIND=8), DIMENSION(:), ALLOCATABLE :: x8n, y8n, z8n
    REAL(KIND=8), DIMENSION(:), ALLOCATABLE :: dvdx, dvdy, dvdz
    REAL(KIND=8) :: hourg
    INTEGER(KIND=4), PARAMETER :: RLK = 8

    REAL(KIND=8) :: coefficient, volinv, ss1, mass1, volume13
    REAL(KIND=8) :: hourmodx, hourmody, hourmodz
    REAL(KIND=8) :: fx_tmp, fy_tmp, fz_tmp
    REAL(KIND=8), DIMENSION(0:7) :: hgfx, hgfy, hgfz
    REAL(KIND=8), DIMENSION(0:7) :: xd1, yd1, zd1
    REAL(KIND=8), DIMENSION(0:3, 0:7) :: hourgam
    REAL(KIND=8), DIMENSION(0:7, 0:3) :: gamma
    REAL(KIND=8), DIMENSION(:), POINTER :: fx_elem, fy_elem, fz_elem
    REAL(KIND=8), DIMENSION(:), POINTER :: fx_local, fy_local, fz_local
    INTEGER(KIND=4) :: numElem, numElem8, i, i2, i3, i1
    INTEGER(KIND=4) :: numNode, ielem
    INTEGER(KIND=4) :: gnode, elem, count, start
    INTEGER(KIND=4) :: offset
    INTEGER(KIND=4) :: n0si2, n1si2, n2si2, n3si2, n4si2, n5si2, n6si2, n7si2
    INTEGER(KIND=4), DIMENSION(:), POINTER :: elemToNode
    INTEGER :: numthreads
    INTEGER, DIMENSION(:), POINTER :: cornerList

    fx_tmp = 0.0_RLK
    fy_tmp = 0.0_RLK
    fz_tmp = 0.0_RLK
    numNode = domain%m_numNode

#if _OPENMP
      numthreads = OMP_GET_MAX_THREADS()
#else
      numthreads = 1
#endif

    NULLIFY(fx_local, fy_local, fz_local)
    NULLIFY(fx_elem, fy_elem, fz_elem)
    numElem = domain%m_numElem
    numElem8 = numElem * 8
    

    IF (numthreads > 1) THEN
      ALLOCATE(fx_elem(0:numElem8-1))
      ALLOCATE(fy_elem(0:numElem8-1))
      ALLOCATE(fz_elem(0:numElem8-1))
    ENDIF

    gamma(0,0) =  1.0_RLK
    gamma(1,0) =  1.0_RLK
    gamma(2,0) = -1.0_RLK
    gamma(3,0) = -1.0_RLK
    gamma(4,0) = -1.0_RLK
    gamma(5,0) = -1.0_RLK
    gamma(6,0) =  1.0_RLK
    gamma(7,0) =  1.0_RLK
    gamma(0,1) =  1.0_RLK
    gamma(1,1) = -1.0_RLK
    gamma(2,1) = -1.0_RLK
    gamma(3,1) =  1.0_RLK
    gamma(4,1) = -1.0_RLK
    gamma(5,1) =  1.0_RLK
    gamma(6,1) =  1.0_RLK
    gamma(7,1) = -1.0_RLK
    gamma(0,2) =  1.0_RLK
    gamma(1,2) = -1.0_RLK
    gamma(2,2) =  1.0_RLK
    gamma(3,2) = -1.0_RLK
    gamma(4,2) =  1.0_RLK
    gamma(5,2) = -1.0_RLK
    gamma(6,2) =  1.0_RLK
    gamma(7,2) = -1.0_RLK
    gamma(0,3) = -1.0_RLK
    gamma(1,3) =  1.0_RLK
    gamma(2,3) = -1.0_RLK
    gamma(3,3) =  1.0_RLK
    gamma(4,3) =  1.0_RLK
    gamma(5,3) = -1.0_RLK
    gamma(6,3) =  1.0_RLK
    gamma(7,3) = -1.0_RLK

  ! *************************************************
  ! compute the hourglass modes

!$OMP PARALLEL DO PRIVATE(i2, elemToNode, fx_local, fy_local, fz_local, hgfx, hgfy,  &
!$OMP                     hgfz, coefficient, hourgam, xd1, yd1, zd1, i3, volinv,     &
!$OMP                     ss1, mass1, volume13, i1, hourmodx, hourmody, hourmodz,    &
!$OMP                     n0si1, n1si2, n2si2, n3si2, n4si2, n5si2, n6si2, n7si2)    &
!$OMP DEFAULT(none) SHARED(domain, determ, gamma, x8n, y8n, z8n)
    DO i2=0, numElem-1

      elemToNode => domain%m_nodelist(i2, :)

      i3=8*i2
      volinv= (1.0_RLK)/determ(i2)

      DO i1=0, 3

        hourmodx =                                             &
          x8n(i3)   * gamma(0,i1) + x8n(i3+1) * gamma(1,i1) +  &
          x8n(i3+2) * gamma(2,i1) + x8n(i3+3) * gamma(3,i1) +  &
          x8n(i3+4) * gamma(4,i1) + x8n(i3+5) * gamma(5,i1) +  &
          x8n(i3+6) * gamma(6,i1) + x8n(i3+7) * gamma(7,i1)

        hourmody =                                             &
          y8n(i3)   * gamma(0,i1) + y8n(i3+1) * gamma(1,i1) +  &
          y8n(i3+2) * gamma(2,i1) + y8n(i3+3) * gamma(3,i1) +  &
          y8n(i3+4) * gamma(4,i1) + y8n(i3+5) * gamma(5,i1) +  &
          y8n(i3+6) * gamma(6,i1) + y8n(i3+7) * gamma(7,i1)

        hourmodz =                                             &
          z8n(i3)   * gamma(0,i1) + z8n(i3+1) * gamma(1,i1) +  &
          z8n(i3+2) * gamma(2,i1) + z8n(i3+3) * gamma(3,i1) +  &
          z8n(i3+4) * gamma(4,i1) + z8n(i3+5) * gamma(5,i1) +  &
          z8n(i3+6) * gamma(6,i1) + z8n(i3+7) * gamma(7,i1)

        hourgam(i1, 0) = gamma(0,i1) -  volinv*(dvdx(i3) * hourmodx +  &
                                                dvdy(i3) * hourmody +  &
                                                dvdz(i3) * hourmodz )
        hourgam(i1, 1) = gamma(1,i1) -  volinv*(dvdx(i3+1) * hourmodx +  &
                                                dvdy(i3+1) * hourmody +  &
                                                dvdz(i3+1) * hourmodz )
        hourgam(i1, 2) = gamma(2,i1) -  volinv*(dvdx(i3+2) * hourmodx +  &
                                                dvdy(i3+2) * hourmody +  &
                                                dvdz(i3+2) * hourmodz )
        hourgam(i1, 3) = gamma(3,i1) -  volinv*(dvdx(i3+3) * hourmodx +  &
                                                dvdy(i3+3) * hourmody +  &
                                                dvdz(i3+3) * hourmodz )
        hourgam(i1, 4) = gamma(4,i1) -  volinv*(dvdx(i3+4) * hourmodx +  &
                                                dvdy(i3+4) * hourmody +  &
                                                dvdz(i3+4) * hourmodz )
        hourgam(i1, 5) = gamma(5,i1) -  volinv*(dvdx(i3+5) * hourmodx +  &
                                                dvdy(i3+5) * hourmody +  &
                                                dvdz(i3+5) * hourmodz )
        hourgam(i1, 6) = gamma(6,i1) -  volinv*(dvdx(i3+6) * hourmodx +  &
                                                dvdy(i3+6) * hourmody +  &
                                                dvdz(i3+6) * hourmodz )
        hourgam(i1, 7) = gamma(7,i1) -  volinv*(dvdx(i3+7) * hourmodx +  &
                                                dvdy(i3+7) * hourmody +  &
                                                dvdz(i3+7) * hourmodz )
      ENDDO

      !   compute forces
      !   store forces into h arrays (force arrays)

      ss1 = domain%m_ss(i2)
      mass1 = domain%m_elemMass(i2)
      volume13 = CBRT(determ(i2))

      n0si2 = elemToNode(0)
      n1si2 = elemToNode(1)
      n2si2 = elemToNode(2)
      n3si2 = elemToNode(3)
      n4si2 = elemToNode(4)
      n5si2 = elemToNode(5)
      n6si2 = elemToNode(6)
      n7si2 = elemToNode(7)

      xd1(0) = domain%m_xd(n0si2)
      xd1(1) = domain%m_xd(n1si2)
      xd1(2) = domain%m_xd(n2si2)
      xd1(3) = domain%m_xd(n3si2)
      xd1(4) = domain%m_xd(n4si2)
      xd1(5) = domain%m_xd(n5si2)
      xd1(6) = domain%m_xd(n6si2)
      xd1(7) = domain%m_xd(n7si2)

      yd1(0) = domain%m_yd(n0si2)
      yd1(1) = domain%m_yd(n1si2)
      yd1(2) = domain%m_yd(n2si2)
      yd1(3) = domain%m_yd(n3si2)
      yd1(4) = domain%m_yd(n4si2)
      yd1(5) = domain%m_yd(n5si2)
      yd1(6) = domain%m_yd(n6si2)
      yd1(7) = domain%m_yd(n7si2)

      zd1(0) = domain%m_zd(n0si2)
      zd1(1) = domain%m_zd(n1si2)
      zd1(2) = domain%m_zd(n2si2)
      zd1(3) = domain%m_zd(n3si2)
      zd1(4) = domain%m_zd(n4si2)
      zd1(5) = domain%m_zd(n5si2)
      zd1(6) = domain%m_zd(n6si2)
      zd1(7) = domain%m_zd(n7si2)

      coefficient = - hourg * (0.01_RLK) * ss1 * mass1 / volume13

      CALL CalcElemFBHourglassForce(xd1, yd1, zd1,          &
                                    hourgam, coefficient,   &
                                    hgfx, hgfy, hgfz)

      IF (numthreads > 1) THEN
        fx_local(0:) => fx_elem(i3:)
        fx_local(0) = hgfx(0)
        fx_local(1) = hgfx(1)
        fx_local(2) = hgfx(2)
        fx_local(3) = hgfx(3)
        fx_local(4) = hgfx(4)
        fx_local(5) = hgfx(5)
        fx_local(6) = hgfx(6)
        fx_local(7) = hgfx(7)

        fy_local(0:) => fy_elem(i3:)
        fy_local(0) = hgfy(0)
        fy_local(1) = hgfy(1)
        fy_local(2) = hgfy(2)
        fy_local(3) = hgfy(3)
        fy_local(4) = hgfy(4)
        fy_local(5) = hgfy(5)
        fy_local(6) = hgfy(6)
        fy_local(7) = hgfy(7)

        fz_local(0:) => fz_elem(i3:)
        fz_local(0) = hgfz(0)
        fz_local(1) = hgfz(1)
        fz_local(2) = hgfz(2)
        fz_local(3) = hgfz(3)
        fz_local(4) = hgfz(4)
        fz_local(5) = hgfz(5)
        fz_local(6) = hgfz(6)
        fz_local(7) = hgfz(7)
      ELSE
        domain%m_fx(n0si2) = domain%m_fx(n0si2) + hgfx(0)
        domain%m_fy(n0si2) = domain%m_fy(n0si2) + hgfy(0)
        domain%m_fz(n0si2) = domain%m_fz(n0si2) + hgfz(0)

        domain%m_fx(n1si2) = domain%m_fx(n1si2) + hgfx(1)
        domain%m_fy(n1si2) = domain%m_fy(n1si2) + hgfy(1)
        domain%m_fz(n1si2) = domain%m_fz(n1si2) + hgfz(1)
        
        domain%m_fx(n2si2) = domain%m_fx(n2si2) + hgfx(2)
        domain%m_fy(n2si2) = domain%m_fy(n2si2) + hgfy(2)
        domain%m_fz(n2si2) = domain%m_fz(n2si2) + hgfz(2)

        domain%m_fx(n3si2) = domain%m_fx(n3si2) + hgfx(3)
        domain%m_fy(n3si2) = domain%m_fy(n3si2) + hgfy(3)
        domain%m_fz(n3si2) = domain%m_fz(n3si2) + hgfz(3)
        
        domain%m_fx(n4si2) = domain%m_fx(n4si2) + hgfx(4)
        domain%m_fy(n4si2) = domain%m_fy(n4si2) + hgfy(4)
        domain%m_fz(n4si2) = domain%m_fz(n4si2) + hgfz(4)
        
        domain%m_fx(n5si2) = domain%m_fx(n5si2) + hgfx(5)
        domain%m_fy(n5si2) = domain%m_fy(n5si2) + hgfy(5)
        domain%m_fz(n5si2) = domain%m_fz(n5si2) + hgfz(5)
        
        domain%m_fx(n6si2) = domain%m_fx(n6si2) + hgfx(6)
        domain%m_fy(n6si2) = domain%m_fy(n6si2) + hgfy(6)
        domain%m_fz(n6si2) = domain%m_fz(n6si2) + hgfz(6)
        
        domain%m_fx(n7si2) = domain%m_fx(n7si2) + hgfx(7)
        domain%m_fy(n7si2) = domain%m_fy(n7si2) + hgfy(7)
        domain%m_fz(n7si2) = domain%m_fz(n7si2) + hgfz(7)
      ENDIF
    ENDDO


    IF (numthreads > 1) THEN
!$OMP PARALLEL DO PRIVATE(gnode, count, cornerList, fx_tmp, fy_tmp, fz_tmp, i, ielem)  &
!$OMP DEFAULT(none) SHARED(domain, fx_elem, fy_elem, fz_elem)
      DO gnode=0, numNode-1
        count = domain%m_nodeElemCount(gnode)
        offset = domain%m_nodeElemStart(gnode)
        fx_tmp = 0.0_RLK
        fy_tmp = 0.0_RLK
        fz_tmp = 0.0_RLK
        DO i=0, count-1
          ielem = domain%m_nodeElemCornerList(offset+i)
          fx_tmp = fx_tmp + fx_elem(ielem)
          fy_tmp = fy_tmp + fy_elem(ielem)
          fz_tmp = fz_tmp + fz_elem(ielem)
        ENDDO
        domain%m_fx(gnode) = domain%m_fx(gnode) + fx_tmp
        domain%m_fy(gnode) = domain%m_fy(gnode) + fy_tmp
        domain%m_fz(gnode) = domain%m_fz(gnode) + fz_tmp
      ENDDO

      ! Deallocate the elems
      DEALLOCATE(fz_elem)
      DEALLOCATE(fy_elem)
      DEALLOCATE(fx_elem)
    ENDIF

  END SUBROUTINE CalcFBHourglassForceForElems



  SUBROUTINE CalcHourglassControlForElems(domain, determ, hgcoef)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    REAL(KIND=8),DIMENSION(0:) :: determ
    REAL(KIND=8) :: hgcoef

    ! Hacky hacky
    INTEGER(KIND=4), PARAMETER :: RLK = 8
    INTEGER(KIND=4), PARAMETER :: VolumeError = -1

    REAL(KIND=8), DIMENSION(:), ALLOCATABLE :: dvdx
    REAL(KIND=8), DIMENSION(:), ALLOCATABLE :: dvdy
    REAL(KIND=8), DIMENSION(:), ALLOCATABLE :: dvdz
    REAL(KIND=8), DIMENSION(:), ALLOCATABLE :: x8n
    REAL(KIND=8), DIMENSION(:), ALLOCATABLE :: y8n
    REAL(KIND=8), DIMENSION(:), ALLOCATABLE :: z8n
    REAL(KIND=8), DIMENSION(0:7) :: x1, y1, z1
    REAL(KIND=8), DIMENSION(0:7) :: pfx, pfy, pfz
    INTEGER(KIND=4) :: numElem, numElem8, i, ii, jj
    INTEGER(KIND=4), DIMENSION(:), POINTER :: elemToNode

    numElem = domain%m_numElem
    numElem8 = numElem * 8
    ALLOCATE(dvdx(0:numElem8-1))
    ALLOCATE(dvdy(0:numElem8-1))
    ALLOCATE(dvdz(0:numElem8-1))
    ALLOCATE(x8n(0:numElem8-1))
    ALLOCATE(y8n(0:numElem8-1))
    ALLOCATE(z8n(0:numElem8-1))
    
    ! start loop over elements
!$OMP PARALLEL DO PRIVATE(i, x1, y1, z1, pfx, pfy, pfz, elemToNode, ii, jj)  &
!$OMP DEFAULT(none) SHARED(domain, determ, numElem)
    DO i=0, numElem-1
      ! Index_t* elemToNode = domain.nodelist(i);
      elemToNode => domain%m_nodelist(i, :)
      CALL CollectDomainNodesToElemNodes(domain, elemToNode, x1, y1, z1)
      
      CALL CalcElemVolumeDerivative(pfx, pfy, pfz, x1, y1, z1)

      !   load into temporary storage for FB Hour Glass control
      DO ii=0, 7
        jj=8*i+ii

        dvdx(jj) = pfx(ii)
        dvdy(jj) = pfy(ii)
        dvdz(jj) = pfz(ii)
        x8n(jj)  = x1(ii)
        y8n(jj)  = y1(ii)
        z8n(jj)  = z1(ii)
      ENDDO

      determ(i) = domain%m_volo(i) * domain%m_v(i)

      !   Do a check for negative volumes
      IF ( domain%m_v(i) <= (0.0_RLK) ) THEN
        CALL luabort(VolumeError)
      ENDIF
    ENDDO


    IF ( hgcoef > (0.0_RLK) ) THEN
      CALL CalcFBHourglassForceForElems(domain, determ, x8n, y8n, &
                                        z8n, dvdx, dvdy, dvdz, hgcoef)
    ENDIF

    DEALLOCATE(z8n)
    DEALLOCATE(y8n)
    DEALLOCATE(x8n)
    DEALLOCATE(dvdz)
    DEALLOCATE(dvdy)
    DEALLOCATE(dvdx)

    RETURN

  END SUBROUTINE CalcHourglassControlForElems



  SUBROUTINE CalcVolumeForceForElems(domain)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    INTEGER(KIND=4) :: numElem
    INTEGER(KIND=4) :: k
    INTEGER(KIND=4), PARAMETER :: RLK = 8
    REAL(KIND=8) :: hgcoef
    REAL(KIND=8), DIMENSION(:), ALLOCATABLE :: sigxx
    REAL(KIND=8), DIMENSION(:), ALLOCATABLE :: sigyy
    REAL(KIND=8), DIMENSION(:), ALLOCATABLE :: sigzz
    REAL(KIND=8), DIMENSION(:), ALLOCATABLE :: determ

    ! Hacky hacky
    INTEGER(KIND=4), PARAMETER :: VolumeError = -1

    numElem = domain%m_numElem
    IF (numElem /= 0) THEN
      hgcoef = domain%m_hgcoef
      ALLOCATE(sigxx(0:numElem-1))
      ALLOCATE(sigyy(0:numElem-1))
      ALLOCATE(sigzz(0:numElem-1))
      ALLOCATE(determ(0:numElem-1))

      ! Sum contributions to total stress tensor
      CALL InitStressTermsForElems(domain, sigxx, sigyy, sigzz, numElem)

      ! Call elemlib stress integration loop to produce nodal forces from
      ! material stresses.
      CALL IntegrateStressForElems(domain, sigxx, sigyy, sigzz, determ, numElem)

      ! Check for negative element volume and abort if found
!$OMP PARALLEL DO PRIVATE(k) DEFAULT(none) SHARED(domain, determ)
      DO k=0, numElem-1
         IF (determ(k) <= 0.0_RLK) THEN
           CALL luabort(VolumeError)
         ENDIF
      ENDDO

      CALL CalcHourglassControlForElems(domain, determ, hgcoef)

      DEALLOCATE(determ)
      DEALLOCATE(sigzz)
      DEALLOCATE(sigyy)
      DEALLOCATE(sigxx)
    ENDIF

  END SUBROUTINE CalcVolumeForceForElems



  SUBROUTINE CalcForceForNodes(domain)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    INTEGER(KIND=4) :: numNode
    INTEGER(KIND=4) :: i
    INTEGER(KIND=4), PARAMETER :: RLK = 8

    numNode = domain%m_numNode

!$OMP PARALLEL DO PRIVATE(i) DEFAULT(none) SHARED(domain)
    DO i=0, numNode-1
      domain%m_fx(i) = 0.0_RLK
      domain%m_fy(i) = 0.0_RLK
      domain%m_fz(i) = 0.0_RLK
    ENDDO

    ! Calcforce calls partial, force, hourq
    CALL CalcVolumeForceForElems(domain)

  END SUBROUTINE CalcForceForNodes


  SUBROUTINE CalcAccelerationForNodes(domain)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    INTEGER(KIND=4) :: numNode
    INTEGER(KIND=4) :: i

    numNode = domain%m_numNode

!  !$OMP PARALLEL DO PRIVATE(i) DEFAULT(none) SHARED(domain)
    DO i=0, numNode-1
      domain%m_xdd(i) = domain%m_fx(i) / domain%m_nodalMass(i)
      domain%m_ydd(i) = domain%m_fy(i) / domain%m_nodalMass(i)
      domain%m_zdd(i) = domain%m_fz(i) / domain%m_nodalMass(i)
    ENDDO

  END SUBROUTINE CalcAccelerationForNodes


  ! NOTE: There are no checks implemented in the FORTRAN version
  !       this needs to be checked with Jan.
  SUBROUTINE ApplyAccelerationBoundaryConditionsForNodes(domain)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    INTEGER(KIND=4) :: numNodeBC
    INTEGER(KIND=4) :: i
    INTEGER(KIND=4), PARAMETER :: RLK = 8

    numNodeBC = (domain%m_sizeX+1)*(domain%m_sizeX+1)

    IF (domain%m_symm_is_set) THEN
!$OMP PARALLEL DO PRIVATE(i) DEFAULT(none) SHARED(domain)
      DO i=0, numNodeBC-1
        domain%m_xdd(domain%m_symmX(i)) = 0.0_RLK
        domain%m_ydd(domain%m_symmY(i)) = 0.0_RLK
        domain%m_zdd(domain%m_symmZ(i)) = 0.0_RLK
      END DO
    ENDIF

  END SUBROUTINE ApplyAccelerationBoundaryConditionsForNodes


  SUBROUTINE CalcVelocityForNodes(domain, dt, u_cut)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    REAL(KIND=8)    :: dt, u_cut
    INTEGER(KIND=4) :: numNode
    INTEGER(KIND=4) :: i
    INTEGER(KIND=4), PARAMETER :: RLK = 8
    REAL(KIND=8)    :: xdtmp, ydtmp, zdtmp

    numNode = domain%m_numNode


!$OMP PARALLEL DO PRIVATE(i, xdtmp, ydtmp, zdtmp) DEFAULT(none)   &
!$OMP SHARED(domain, dt, u_cut)
    DO i = 0, numNode-1

      xdtmp = domain%m_xd(i) + domain%m_xdd(i) * dt
      IF( ABS(xdtmp) < u_cut ) THEN
        xdtmp = 0.0_RLK
      ENDIF
      domain%m_xd(i) = xdtmp

      ydtmp = domain%m_yd(i) + domain%m_ydd(i) * dt
      IF( ABS(ydtmp) < u_cut ) THEN
        ydtmp = 0.0_RLK
      ENDIF
      domain%m_yd(i) = ydtmp

      zdtmp = domain%m_zd(i) + domain%m_zdd(i) * dt
      IF( ABS(zdtmp) < u_cut ) THEN
        zdtmp = 0.0_RLK
      ENDIF
      domain%m_zd(i) = zdtmp
    ENDDO

  END SUBROUTINE CalcVelocityForNodes



  SUBROUTINE CalcPositionForNodes(domain, dt)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    REAL(KIND=8)    :: dt
    INTEGER(KIND=4) :: numNode
    INTEGER(KIND=4) :: i

    numNode = domain%m_numNode

!$OMP PARALLEL DO PRIVATE(i) DEFAULT(none) SHARED(domain, dt)
    DO i = 0, numNode-1
      domain%m_x(i) = domain%m_x(i) + domain%m_xd(i) * dt
      domain%m_y(i) = domain%m_y(i) + domain%m_yd(i) * dt
      domain%m_z(i) = domain%m_z(i) + domain%m_zd(i) * dt
    ENDDO

  END SUBROUTINE CalcPositionForNodes



  SUBROUTINE LagrangeNodal(domain)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    REAL(KIND=8) :: delt
    REAL(KIND=8) :: u_cut

    delt = domain%m_deltatime
    u_cut = domain%m_u_cut

    ! Time of boundary condition evaluation is beginning of
    ! step for force and acceleration boundary conditions.
    CALL CalcForceForNodes(domain)

    CALL CalcAccelerationForNodes(domain)

    CALL ApplyAccelerationBoundaryConditionsForNodes(domain)

    CALL CalcVelocityForNodes(domain, delt, u_cut)

    CALL CalcPositionForNodes(domain, delt)

  END SUBROUTINE LagrangeNodal



  FUNCTION CalcElemCharacteristicLength( x, y, z, volume) RESULT(charLength)
    IMPLICIT NONE

    REAL(KIND=8), DIMENSION(0:7) :: x, y, z
    REAL(KIND=8) :: volume
    REAL(KIND=8) :: a
    INTEGER(KIND=4), PARAMETER :: RLK = 8
    REAL(KIND=8) :: charLength

    charLength = 0.0_RLK

    a = AreaFace(x(0), x(1), x(2), x(3),  &
                 y(0), y(1), y(2), y(3),  &
                 z(0), z(1), z(2), z(3))
    charLength = MAX(a, charLength)
    
    a = AreaFace(x(4), x(5), x(6), x(7),  &
                 y(4), y(5), y(6), y(7),  &
                 z(4), z(5), z(6), z(7))
    charLength = MAX(a, charLength)
    
    a = AreaFace(x(0), x(1), x(5), x(4),  &
                 y(0), y(1), y(5), y(4),  &
                 z(0), z(1), z(5), z(4))
    charLength = MAX(a, charLength)
    
    a = AreaFace(x(1), x(2), x(6), x(5),  &
                 y(1), y(2), y(6), y(5),  &
                 z(1), z(2), z(6), z(5))
    charLength = MAX(a, charLength)
    
    a = AreaFace(x(2), x(3), x(7), x(6),  &
                 y(2), y(3), y(7), y(6),  &
                 z(2), z(3), z(7), z(6))
    charLength = MAX(a, charLength)
    
    a = AreaFace(x(3), x(0), x(4), x(7),  &
                 y(3), y(0), y(4), y(7),  &
                 z(3), z(0), z(4), z(7))
    charLength = MAX(a, charLength)

    charLength = (4.0_RLK) * volume / SQRT(charLength);

    RETURN

  END FUNCTION CalcElemCharacteristicLength



  SUBROUTINE CalcElemVelocityGrandient( xvel, yvel, zvel, &
                                        b, detJ, d )
    IMPLICIT NONE 

    REAL(KIND=8), DIMENSION(0:7),     INTENT(IN)  :: xvel, yvel, zvel
    REAL(KIND=8), DIMENSION(0:7,0:2), INTENT(IN)  :: b
    REAL(KIND=8),                     INTENT(IN)  :: detJ
    REAL(KIND=8), DIMENSION(0:5),     INTENT(OUT) :: d
    INTEGER(KIND=4), PARAMETER :: RLK = 8

    REAL(KIND=8) :: dyddx, dxddy, dzddx, dxddz, dzddy, dyddz
    REAL(KIND=8) :: inv_detJ
    REAL(KIND=8), DIMENSION(0:7) :: pfx
    REAL(KIND=8), DIMENSION(0:7) :: pfy
    REAL(KIND=8), DIMENSION(0:7) :: pfz

    inv_detJ = (1.0_RLK) / detJ
    pfx = b(:, 0)
    pfy = b(:, 1)
    pfz = b(:, 2)

    d(0) = inv_detJ * ( pfx(0) * (xvel(0)-xvel(6))   &
                      + pfx(1) * (xvel(1)-xvel(7))   &
                      + pfx(2) * (xvel(2)-xvel(4))   &
                      + pfx(3) * (xvel(3)-xvel(5)) )

    d(1) = inv_detJ * ( pfy(0) * (yvel(0)-yvel(6))   &
                      + pfy(1) * (yvel(1)-yvel(7))   &
                      + pfy(2) * (yvel(2)-yvel(4))   &
                      + pfy(3) * (yvel(3)-yvel(5)) )

    d(2) = inv_detJ * ( pfz(0) * (zvel(0)-zvel(6))   &
                      + pfz(1) * (zvel(1)-zvel(7))   &
                      + pfz(2) * (zvel(2)-zvel(4))   &
                      + pfz(3) * (zvel(3)-zvel(5)) )

    dyddx = inv_detJ * ( pfx(0) * (yvel(0)-yvel(6))  &
                       + pfx(1) * (yvel(1)-yvel(7))  &
                       + pfx(2) * (yvel(2)-yvel(4))  &
                       + pfx(3) * (yvel(3)-yvel(5)) )

    dxddy = inv_detJ * ( pfy(0) * (xvel(0)-xvel(6))  &
                       + pfy(1) * (xvel(1)-xvel(7))  &
                       + pfy(2) * (xvel(2)-xvel(4))  &
                       + pfy(3) * (xvel(3)-xvel(5)) )

    dzddx = inv_detJ * ( pfx(0) * (zvel(0)-zvel(6))  &
                       + pfx(1) * (zvel(1)-zvel(7))  &
                       + pfx(2) * (zvel(2)-zvel(4))  &
                       + pfx(3) * (zvel(3)-zvel(5)) )

    dxddz = inv_detJ * ( pfz(0) * (xvel(0)-xvel(6))  &
                       + pfz(1) * (xvel(1)-xvel(7))  &
                       + pfz(2) * (xvel(2)-xvel(4))  &
                       + pfz(3) * (xvel(3)-xvel(5)) )

    dzddy = inv_detJ * ( pfy(0) * (zvel(0)-zvel(6))  &
                       + pfy(1) * (zvel(1)-zvel(7))  &
                       + pfy(2) * (zvel(2)-zvel(4))  &
                       + pfy(3) * (zvel(3)-zvel(5)) )

    dyddz = inv_detJ * ( pfz(0) * (yvel(0)-yvel(6))  &
                       + pfz(1) * (yvel(1)-yvel(7))  &
                       + pfz(2) * (yvel(2)-yvel(4))  &
                       + pfz(3) * (yvel(3)-yvel(5)) )
    d(5) = (0.5_RLK) * ( dxddy + dyddx )
    d(4) = (0.5_RLK) * ( dxddz + dzddx )
    d(3) = (0.5_RLK) * ( dzddy + dyddz )

  END SUBROUTINE CalcElemVelocityGrandient


  SUBROUTINE CalcKinematicsForElems(domain, dt, numElem)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    INTEGER      :: numElem
    INTEGER      :: k, lnode, gnode, j
    REAL(KIND=8) :: dt
    INTEGER(KIND=4), PARAMETER :: RLK = 8

    REAL(KIND=8), DIMENSION(0:7,0:2) :: B  ! shape function derivatives
    REAL(KIND=8), DIMENSION(0:5):: D
    REAL(KIND=8), DIMENSION(0:7) :: x_local
    REAL(KIND=8), DIMENSION(0:7) :: y_local
    REAL(KIND=8), DIMENSION(0:7) :: z_local
    REAL(KIND=8), DIMENSION(0:7) :: xd_local
    REAL(KIND=8), DIMENSION(0:7) :: yd_local
    REAL(KIND=8), DIMENSION(0:7) :: zd_local
    REAL(KIND=8) :: detJ, volume, relativeVolume,dt2
    INTEGER(KIND=4), DIMENSION(:), POINTER :: elemToNode => NULL()

    detJ = 0.0_RLK

    ! Loop over all elements
!$OMP PARALLEL DO PRIVATE(k, B, D, x_local, y_local, z_local, xd_local, yd_local,  &
!$OMP                     zd_local, detJ, volume, relativeVolume, elemToNode,      &
!$OMP                     lnode, gnode, dt2, j)                                    &
!$OMP DEFAULT(none) SHARED(domain)
    DO k = 0, numElem-1
      elemToNode => domain%m_nodelist(k, :)

      ! Get nodal coordinates from global arrays and copy into local arrays
      CALL CollectDomainNodesToElemNodes(domain, elemToNode, &
                                    x_local, y_local, z_local)

      ! Volume calculations
      volume = CalcElemVolume(x_local, y_local, z_local )
      relativeVolume = volume / domain%m_volo(k)
      domain%m_vnew(k) = relativeVolume
      domain%m_delv(k) = relativeVolume - domain%m_v(k)

      ! Set characteristic length
      domain%m_arealg(k) = CalcElemCharacteristicLength(x_local, y_local,  &
                                                        z_local, volume)

      ! Get nodal velocities from global array and copy into local arrays.
      DO lnode=0, 7
        gnode = elemToNode(lnode);
        xd_local(lnode) = domain%m_xd(gnode)
        yd_local(lnode) = domain%m_yd(gnode)
        zd_local(lnode) = domain%m_zd(gnode)
      ENDDO

      dt2 = (0.5_RLK) * dt
      DO j=0, 7
        x_local(j) = x_local(j) - dt2 * xd_local(j)
        y_local(j) = y_local(j) - dt2 * yd_local(j)
        z_local(j) = z_local(j) - dt2 * zd_local(j)
      ENDDO

      CALL CalcElemShapeFunctionDerivatives( x_local, y_local, z_local,  &
                                             B, detJ )

      CALL CalcElemVelocityGrandient( xd_local, yd_local, zd_local,  &
                                      B, detJ, D )

      ! Put velocity gradient quantities into their global arrays.
      domain%m_dxx(k) = D(0);
      domain%m_dyy(k) = D(1);
      domain%m_dzz(k) = D(2);
    ENDDO

  END SUBROUTINE CalcKinematicsForElems



  SUBROUTINE CalcLagrangeElements(domain)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    REAL(KIND=8)    :: deltatime
    REAL(KIND=8)    :: vdov, vdovthird
    INTEGER(KIND=4) :: numElem, k

    ! Hacky hacky
    INTEGER(KIND=4), PARAMETER :: RLK = 8
    INTEGER(KIND=4), PARAMETER :: VolumeError = -1

    deltatime = domain%m_deltatime

    numElem = domain%m_numElem
    IF (numElem > 0) THEN
      deltatime = domain%m_deltatime

      CALL AllocateStrains(domain, numElem)

      CALL CalcKinematicsForElems(domain, deltatime, numElem)

      ! Element loop to do some stuff not included in the elemlib function.
!$OMP PARALLEL DO PRIVATE(k, vdov, vdovthird) DEFAULT(none) SHARED(domain)
      DO k=0, numElem-1
        ! Calc strain rate and apply as constraint (only done in FB element)
        vdov = domain%m_dxx(k) + domain%m_dyy(k) + domain%m_dzz(k)
        vdovthird = vdov/(3.0_RLK)

        ! Make the rate of deformation tensor deviatoric
        domain%m_vdov(k) = vdov
        domain%m_dxx(k) = domain%m_dxx(k) - vdovthird
        domain%m_dyy(k) = domain%m_dyy(k) - vdovthird
        domain%m_dzz(k) = domain%m_dzz(k) - vdovthird

        ! See if any volumes are negative, and take appropriate action.
        IF (domain%m_vnew(k) <= (0.0_RLK)) THEN
          call luabort(VolumeError)
        ENDIF
      ENDDO

      ! Deallocate the strains
      CALL DeallocateStrains(domain)
    ENDIF

  END SUBROUTINE CalcLagrangeElements



  SUBROUTINE  CalcMonotonicQGradientsForElems(domain)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    INTEGER(KIND=4), PARAMETER :: RLK = 8
    REAL(KIND=8), PARAMETER :: ptiny = 1.e-36_RLK
    REAL(KIND=8)            :: ax, ay, az, dxv, dyv, dzv
    REAL(KIND=8)            :: x0, x1, x2, x3, x4, x5, x6, x7
    REAL(KIND=8)            :: y0, y1, y2, y3, y4, y5, y6, y7
    REAL(KIND=8)            :: z0, z1, z2, z3, z4, z5, z6, z7
    REAL(KIND=8)            :: xv0, xv1, xv2, xv3, xv4, xv5, xv6, xv7
    REAL(KIND=8)            :: yv0, yv1, yv2, yv3, yv4, yv5, yv6, yv7
    REAL(KIND=8)            :: zv0, zv1, zv2, zv3, zv4, zv5, zv6, zv7
    REAL(KIND=8)            :: vol, norm
    REAL(KIND=8)            :: dxi, dxj, dxk
    REAL(KIND=8)            :: dyi, dyj, dyk
    REAL(KIND=8)            :: dzi, dzj, dzk
    INTEGER(KIND=4)         :: numElem, i
    INTEGER(KIND=4)         :: n0, n1, n2, n3, n4, n5, n6, n7
    INTEGER(KIND=4), DIMENSION(:), POINTER :: elemToNode => NULL()

    numElem = domain%m_numElem

!$OMP PARALLEL DO PRIVATE(i, ptiny, ax, ay, az, dxv, dyv, dzv, elemToNode, n0,  &
!$OMP                     n1, n2, n3, n4, n5, n6, n7, x0, x1, x2, x3, x4, x5,   &
!$OMP                     x6, x7, y0, y1, y2, y3, y4, y5, y6, y7, z0, z1, z2,   &
!$OMP                     z3, z4, z5, z6, z7, xv0, xv1, xv2, xv3, xv4, xv5,     &
!$OMP                     xv6, xv7, yv0, yv1, yv2, yv3, yv4, yv5, yv6, yv7,     &
!$OMP                     zv0, zv1, zv2, zv3, zv4, zv5, zv6, zv7, vol, norm,    &
!$OMP                     dxj, dyj, dzj, dxi, dyi, dzi, dxk, dyk, dzk)          &
!$OMP DEFAULT(none) SHARED(domain, ptiny)
    DO i=0, numElem-1

      elemToNode => domain%m_nodelist(i, :)
      n0 = elemToNode(0)
      n1 = elemToNode(1)
      n2 = elemToNode(2)
      n3 = elemToNode(3)
      n4 = elemToNode(4)
      n5 = elemToNode(5)
      n6 = elemToNode(6)
      n7 = elemToNode(7)

      x0 = domain%m_x(n0)
      x1 = domain%m_x(n1)
      x2 = domain%m_x(n2)
      x3 = domain%m_x(n3)
      x4 = domain%m_x(n4)
      x5 = domain%m_x(n5)
      x6 = domain%m_x(n6)
      x7 = domain%m_x(n7)

      y0 = domain%m_y(n0)
      y1 = domain%m_y(n1)
      y2 = domain%m_y(n2)
      y3 = domain%m_y(n3)
      y4 = domain%m_y(n4)
      y5 = domain%m_y(n5)
      y6 = domain%m_y(n6)
      y7 = domain%m_y(n7)

      z0 = domain%m_z(n0)
      z1 = domain%m_z(n1)
      z2 = domain%m_z(n2)
      z3 = domain%m_z(n3)
      z4 = domain%m_z(n4)
      z5 = domain%m_z(n5)
      z6 = domain%m_z(n6)
      z7 = domain%m_z(n7)

      xv0 = domain%m_xd(n0)
      xv1 = domain%m_xd(n1)
      xv2 = domain%m_xd(n2)
      xv3 = domain%m_xd(n3)
      xv4 = domain%m_xd(n4)
      xv5 = domain%m_xd(n5)
      xv6 = domain%m_xd(n6)
      xv7 = domain%m_xd(n7)

      yv0 = domain%m_yd(n0)
      yv1 = domain%m_yd(n1)
      yv2 = domain%m_yd(n2)
      yv3 = domain%m_yd(n3)
      yv4 = domain%m_yd(n4)
      yv5 = domain%m_yd(n5)
      yv6 = domain%m_yd(n6)
      yv7 = domain%m_yd(n7)

      zv0 = domain%m_zd(n0)
      zv1 = domain%m_zd(n1)
      zv2 = domain%m_zd(n2)
      zv3 = domain%m_zd(n3)
      zv4 = domain%m_zd(n4)
      zv5 = domain%m_zd(n5)
      zv6 = domain%m_zd(n6)
      zv7 = domain%m_zd(n7)

      vol = domain%m_volo(i) * domain%m_vnew(i)
      norm = (1.0_RLK) / ( vol + ptiny )

      dxj = (-0.25_RLK) * ((x0 + x1 + x5 + x4) - &
                           (x3 + x2 + x6 + x7))
      dyj = (-0.25_RLK) * ((y0 + y1 + y5 + y4) - &
                           (y3 + y2 + y6 + y7))
      dzj = (-0.25_RLK) * ((z0 + z1 + z5 + z4) - &
                           (z3 + z2 + z6 + z7))

      dxi = ( 0.25_RLK) * ((x1 + x2 + x6 + x5) - &
                           (x0 + x3 + x7 + x4))
      dyi = ( 0.25_RLK) * ((y1 + y2 + y6 + y5) - &
                           (y0 + y3 + y7 + y4))
      dzi = ( 0.25_RLK) * ((z1 + z2 + z6 + z5) - &
                           (z0 + z3 + z7 + z4))

      dxk = ( 0.25_RLK) * ((x4 + x5 + x6 + x7) - &
                           (x0 + x1 + x2 + x3))
      dyk = ( 0.25_RLK) * ((y4 + y5 + y6 + y7) - &
                           (y0 + y1 + y2 + y3))
      dzk = ( 0.25_RLK) * ((z4 + z5 + z6 + z7) - &
                           (z0 + z1 + z2 + z3))

      ! Find delvk and delxk ( i cross j )
      ax = dyi * dzj - dzi * dyj
      ay = dzi * dxj - dxi * dzj
      az = dxi * dyj - dyi * dxj

      domain%m_delx_zeta(i) = vol / SQRT(ax*ax + ay*ay + az*az + ptiny)

      ax = ax * norm
      ay = ay * norm
      az = az * norm

      dxv = (0.25_RLK) * ((xv4 + xv5 + xv6 + xv7) - &
                          (xv0 + xv1 + xv2 + xv3))
      dyv = (0.25_RLK) * ((yv4 + yv5 + yv6 + yv7) - &
                          (yv0 + yv1 + yv2 + yv3))
      dzv = (0.25_RLK) * ((zv4 + zv5 + zv6 + zv7) - &
                          (zv0 + zv1 + zv2 + zv3))

      domain%m_delv_zeta(i) = ax*dxv + ay*dyv + az*dzv

      ! Find delxi and delvi ( j cross k )
      ax = dyj * dzk - dzj * dyk ;
      ay = dzj * dxk - dxj * dzk ;
      az = dxj * dyk - dyj * dxk ;

      domain%m_delx_xi(i) = vol / SQRT(ax*ax + ay*ay + az*az + ptiny) ;

      ax = ax * norm
      ay = ay * norm
      az = az * norm

      dxv = (0.25_RLK) * ((xv1 + xv2 + xv6 + xv5) - &
                          (xv0 + xv3 + xv7 + xv4))
      dyv = (0.25_RLK) * ((yv1 + yv2 + yv6 + yv5) - &
                          (yv0 + yv3 + yv7 + yv4))
      dzv = (0.25_RLK) * ((zv1 + zv2 + zv6 + zv5) - &
                          (zv0 + zv3 + zv7 + zv4))

      domain%m_delv_xi(i) = ax*dxv + ay*dyv + az*dzv ;

      ! Find delxj and delvj ( k cross i )
      ax = dyk * dzi - dzk * dyi
      ay = dzk * dxi - dxk * dzi
      az = dxk * dyi - dyk * dxi

      domain%m_delx_eta(i) = vol / SQRT(ax*ax + ay*ay + az*az + ptiny) ;

      ax = ax * norm
      ay = ay * norm
      az = az * norm

      dxv = (-0.25_RLK) * ((xv0 + xv1 + xv5 + xv4) - &
                           (xv3 + xv2 + xv6 + xv7)) ;
      dyv = (-0.25_RLK) * ((yv0 + yv1 + yv5 + yv4) - &
                           (yv3 + yv2 + yv6 + yv7)) ;
      dzv = (-0.25_RLK) * ((zv0 + zv1 + zv5 + zv4) - &
                           (zv3 + zv2 + zv6 + zv7)) ;

      domain%m_delv_eta(i) = ax*dxv + ay*dyv + az*dzv ;
    ENDDO
    !  !$OMP END PARALLEL DO

  END SUBROUTINE CalcMonotonicQGradientsForElems



  ! TODO(Ludger): Check this function further, this is an easy way to mess up
  SUBROUTINE CalcMonotonicQRegionForElems(domain, r, ptiny) 
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    REAL(KIND=8) :: qlc_monoq,  qqc_monoq
    REAL(KIND=8) :: monoq_limiter_mult,  monoq_max_slope
    REAL(KIND=8) :: ptiny
    INTEGER(KIND=4), PARAMETER :: RLK = 8
    INTEGER(KIND=4) :: r
    INTEGER(KIND=4) :: ielem, i, bcMask
    REAL(KIND=8) :: qlin, qquad, phixi, phieta, phizeta, delvm, delvp
    REAL(KIND=8) :: norm, delvxxi, delvxeta, delvxzeta, rho

    ! Hacky introduction of the necessary information for the boundary conditions
    INTEGER, PARAMETER :: XI_M        = int(z'003',kind=rlk) ! 0x003
    INTEGER, PARAMETER :: XI_M_SYMM   = int(z'001', kind=rlk) ! 0x001
    INTEGER, PARAMETER :: XI_M_FREE   = int(z'002', kind=rlk) ! 0x002

    INTEGER, PARAMETER :: XI_P        = int(z'00c', kind=rlk) ! 0x00c
    INTEGER, PARAMETER :: XI_P_SYMM   = int(z'004', kind=rlk) ! 0x004
    INTEGER, PARAMETER :: XI_P_FREE   = int(z'008', kind=rlk) ! 0x008

    INTEGER, PARAMETER :: ETA_M       = int(z'030', kind=rlk) ! 0x030
    INTEGER, PARAMETER :: ETA_M_SYMM  = int(z'010', kind=rlk) ! 0x010
    INTEGER, PARAMETER :: ETA_M_FREE  = int(z'020', kind=rlk) ! 0x020

    INTEGER, PARAMETER :: ETA_P       = int(z'0c0', kind=rlk) ! 0x0c0
    INTEGER, PARAMETER :: ETA_P_SYMM  = int(z'040', kind=rlk) ! 0x040
    INTEGER, PARAMETER :: ETA_P_FREE  = int(z'080', kind=rlk) ! 0x080

    INTEGER, PARAMETER :: ZETA_M      = int(z'300', kind=rlk) ! 0x300
    INTEGER, PARAMETER :: ZETA_M_SYMM = int(z'100', kind=rlk) ! 0x100
    INTEGER, PARAMETER :: ZETA_M_FREE = int(z'200', kind=rlk) ! 0x200

    INTEGER, PARAMETER :: ZETA_P      = int(z'c00', kind=rlk) ! 0xc00
    INTEGER, PARAMETER :: ZETA_P_SYMM = int(z'400', kind=rlk) ! 0x400
    INTEGER, PARAMETER :: ZETA_P_FREE = int(z'800', kind=rlk) ! 0x800

    monoq_limiter_mult = domain%m_monoq_limiter_mult
    monoq_max_slope = domain%m_monoq_max_slope
    qlc_monoq = domain%m_qlc_monoq
    qqc_monoq = domain%m_qqc_monoq


!$OMP PARALLEL DO PRIVATE(i, ielem, qlin, qquad, phixi, phieta, phizeta, bcMask,   &
!$OMP                     delvm, norm, delvxxi, delvxeta, delvxzeta, rho)          &
!$OMP DEFAULT(none) SHARED(domain, ptiny, XI_M, XI_M_SYMM, XI_M_FREE, XI_P,        &
!$OMP                      XI_P_COMM, XI_P_SYMM, XI_P_FREE, monoq_limiter_mult,    &
!$OMP                      monoq_max_slope, ETA_M, ETA_M_COMM, ETA_M_SYMM,         &
!$OMP                      ETA_M_FREE, ETA_P, ETA_P_COMM, ETA_P_SYMM, ETA_P_FREE,  &
!$OMP                      ZETA_M, ZETA_M_SYMM, ZETA_M_FREE, ZETA_P, ZETA_P_COMM,  &
!$OMP                      ZETA_P_SYMM, ZETA_P_FREE)
    DO i=0, domain%m_regElemSize(r)-1
      ielem = domain%m_regElemlist(domain%m_regElemKeys(r) + i)
      !ielem = domain%m_regElemlist(i)
      bcMask = domain%m_elemBC(ielem)

      ! Phixi
      norm = (1.0_RLK) / ( domain%m_delv_xi(ielem) + ptiny )

      SELECT CASE(IAND(bcMask, XI_M))
        CASE (0)
          delvm = domain%m_delv_xi(domain%m_lxim(ielem))
        CASE (XI_M_SYMM)
          delvm = domain%m_delv_xi(ielem)
        CASE (XI_M_FREE)
          delvm = (0.0_RLK)
        CASE DEFAULT
        ! ERROR
      END SELECT

      SELECT CASE(IAND(bcMask, XI_P))
        CASE (0)
          delvp = domain%m_delv_xi(domain%m_lxip(ielem))
        CASE (XI_P_SYMM)
          delvp = domain%m_delv_xi(ielem)
        CASE (XI_P_FREE)
          delvp = (0.0_RLK)
        CASE DEFAULT
        ! ERROR 
      END SELECT

      delvm = delvm * norm
      delvp = delvp * norm

      phixi = (0.5_RLK) * ( delvm + delvp )

      delvm = delvm * monoq_limiter_mult
      delvp = delvp * monoq_limiter_mult

      IF ( delvm < phixi ) THEN 
        phixi = delvm
      ENDIF
      IF ( delvp < phixi ) THEN
        phixi = delvp
      ENDIF
      IF ( phixi < 0.0_RLK ) THEN
        phixi = (0.0_RLK)
      ENDIF
      IF ( phixi > monoq_max_slope) THEN
        phixi = monoq_max_slope
      ENDIF

      ! phieta
      norm = (1.0_RLK) / ( domain%m_delv_eta(ielem) + ptiny )

      SELECT CASE(IAND(bcMask, ETA_M))
        CASE (0)
          delvm = domain%m_delv_eta(domain%m_letam(ielem))
        CASE (ETA_M_SYMM)
          delvm = domain%m_delv_eta(ielem)
        CASE (ETA_M_FREE)
          delvm = 0.0_RLK
        CASE DEFAULT
        ! ERROR
      END SELECT
      SELECT CASE(IAND(bcMask, ETA_P))
        CASE (0)
          delvp = domain%m_delv_eta(domain%m_letap(ielem))
        CASE (ETA_P_SYMM)
          delvp = domain%m_delv_eta(ielem)
        CASE (ETA_P_FREE)
          delvp = (0.0_RLK)
        CASE DEFAULT
        ! ERROR
      END SELECT

      delvm = delvm * norm
      delvp = delvp * norm

      phieta = (0.5_RLK) * ( delvm + delvp )

      delvm = delvm * monoq_limiter_mult
      delvp = delvp * monoq_limiter_mult

      IF ( delvm  < phieta ) THEN
        phieta = delvm
      ENDIF
      IF ( delvp  < phieta ) THEN
        phieta = delvp
      ENDIF
      IF ( phieta < (0.0_RLK)) THEN
        phieta = (0.0_RLK)
      ENDIF
      IF ( phieta > monoq_max_slope) THEN
        phieta = monoq_max_slope
      ENDIF

      ! phizeta
      norm = (1.0_RLK) / ( domain%m_delv_zeta(ielem) + ptiny ) ;

      SELECT CASE(IAND(bcMask, ZETA_M))
        CASE (0)
          delvm = domain%m_delv_zeta(domain%m_lzetam(ielem))
        CASE (ZETA_M_SYMM)
          delvm = domain%m_delv_zeta(ielem)
        CASE (ZETA_M_FREE)
          delvm = (0.0_RLK)
        CASE DEFAULT
        ! ERROR
      END SELECT
      SELECT CASE(IAND(bcMask, ZETA_P))
        CASE (0)
          delvp = domain%m_delv_zeta(domain%m_lzetap(ielem))
        CASE (ZETA_P_SYMM)
          delvp = domain%m_delv_zeta(ielem)
        CASE (ZETA_P_FREE)
          delvp = (0.0_RLK)
        CASE DEFAULT
        ! ERROR
      END SELECT

      delvm = delvm * norm
      delvp = delvp * norm

      phizeta = (0.5_RLK) * ( delvm + delvp )

      delvm = delvm * monoq_limiter_mult
      delvp = delvp * monoq_limiter_mult

      IF ( delvm   < phizeta ) THEN
        phizeta = delvm
      ENDIF
      IF ( delvp   < phizeta ) THEN
        phizeta = delvp
      ENDIF
      IF ( phizeta < (0.0_RLK) ) THEN
        phizeta = (0.0_RLK)
      ENDIF
      IF ( phizeta > monoq_max_slope  ) THEN
        phizeta = monoq_max_slope
      ENDIF

      ! Remove length scale

      IF ( domain%m_vdov(ielem) > (0.0_RLK) ) THEN
        qlin  = (0.0_RLK)
        qquad = (0.0_RLK)
      ELSE
        delvxxi   = domain%m_delv_xi(ielem)   * domain%m_delx_xi(ielem)
        delvxeta  = domain%m_delv_eta(ielem)  * domain%m_delx_eta(ielem)
        delvxzeta = domain%m_delv_zeta(ielem) * domain%m_delx_zeta(ielem)

        IF ( delvxxi   > (0.0_RLK) ) THEN
          delvxxi   = (0.0_RLK)
        ENDIF
        IF ( delvxeta  > (0.0_RLK) ) THEN
          delvxeta  = (0.0_RLK)
        ENDIF
        IF ( delvxzeta > (0.0_RLK) ) THEN
          delvxzeta = (0.0_RLK)
        ENDIF

        rho = domain%m_elemMass(ielem) / (domain%m_volo(ielem) * domain%m_vnew(ielem))

        qlin = -qlc_monoq * rho *                      &
               (  delvxxi   * ((1.0_RLK) - phixi)  +     &
                  delvxeta  * ((1.0_RLK) - phieta) +     &
                  delvxzeta * ((1.0_RLK) - phizeta)  )

        qquad = qqc_monoq * rho *                                       &
               (  delvxxi*delvxxi     * ((1.0_RLK) - phixi*phixi)   +     &
                  delvxeta*delvxeta   * ((1.0_RLK) - phieta*phieta) +     &
                  delvxzeta*delvxzeta * ((1.0_RLK) - phizeta*phizeta)  )
      ENDIF

      domain%m_qq(ielem) = qquad
      domain%m_ql(ielem) = qlin
    ENDDO

  END SUBROUTINE CalcMonotonicQRegionForElems



  SUBROUTINE CalcMonotonicQForElems(domain)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    INTEGER(KIND=4), PARAMETER :: RLK = 8
    REAL(KIND=8), PARAMETER :: ptiny = 1.e-36_RLK
    REAL(KIND=8) :: monoq_max_slope
    REAL(KIND=8) :: monoq_limiter_mult
    REAL(KIND=8) :: qlc_monoq
    REAL(KIND=8) :: qqc_monoq
    INTEGER(KIND=4) :: r

    !
    ! calculate the monotonic q for pure regions
    !
    DO r=0, domain%m_numReg-1
      IF (domain%m_regElemSize(r) > 0) THEN
        CALL CalcMonotonicQRegionForElems(domain, r, ptiny)
      ENDIF
    ENDDO

  END SUBROUTINE CalcMonotonicQForElems



  SUBROUTINE CalcQForElems(domain)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    REAL(KIND=8)    :: qstop
    INTEGER(KIND=4) :: numElem, allElem, idx, i

    INTEGER(KIND=4), PARAMETER :: QStopError  = -2

    qstop = domain%m_qstop
    numElem = domain%m_numElem
    !
    ! MONOTONIC Q option
    !

    IF (numElem /= 0) THEN
      allElem = numElem +                           &
                2 * domain%m_sizeX*domain%m_sizeY + &  ! Plane ghosts
                2 * domain%m_sizeX*domain%m_sizeZ + &  ! Row ghosts
                2 * domain%m_sizeY*domain%m_sizeZ   ! Col ghosts

      CALL AllocateGradients(domain, numElem, allElem)

      CALL CalcMonotonicQGradientsForElems(domain)

      CALL CalcMonotonicQForElems(domain)

      ! Free up memory
      CALL DeallocateGradients(domain)

      ! Don't allow excessive artificial viscosity
      idx = -1
      DO i=0, numElem-1
        IF (domain%m_ql(i) > qstop) THEN
          idx = i
          EXIT
        ENDIF
      ENDDO

      IF (idx >= 0) THEN
        CALL luabort(QStopError)
      ENDIF
    ENDIF

  END SUBROUTINE CalcQForElems



  SUBROUTINE CalcPressureForElems( domain, p_new, bvc, &
                                   pbvc, e_old,        &
                                   compression, vnewc, &
                                   pmin,               &
                                   p_cut,eosvmax,      &
                                   length              )

    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    REAL(KIND=8), DIMENSION(0:) :: p_new, bvc, pbvc, e_old
    REAL(KIND=8), DIMENSION(0:) ::  compression
    REAL(KIND=8), DIMENSION(0:) :: vnewc
    INTEGER(KIND=4), PARAMETER :: RLK = 8
    REAL(KIND=8)    :: pmin
    REAL(KIND=8)    :: p_cut
    REAL(KIND=8)    :: eosvmax
    INTEGER(KIND=4) :: length 

    INTEGER(KIND=4) :: i, ielem
    REAL(KIND=8) :: c1s

!$OMP PARALLEL DO PRIVATE(i, c1s) DEFAULT(none) SHARED(bvc, pbvc, compression)
    DO i = 0, length-1
      c1s = (2.0_RLK)/(3.0_RLK)
      bvc(i) = c1s * (compression(i) + (1.0_RLK))
      pbvc(i) = c1s
    ENDDO

!$OMP PARALLEL DO PRIVATE(i, ielem) DEFAULT(none)   &
!$OMP SHARED(domain, p_new, bvc, e_old, p_cut, vnewc, eosvmax, pmin)
    DO i = 0, length-1
      ielem = domain%m_regElemlist(i)

      p_new(i) = bvc(i) * e_old(i)

      IF (ABS(p_new(i)) < p_cut) THEN
        p_new(i) = (0.0_RLK)
      ENDIF

      IF ( vnewc(ielem) >= eosvmax ) THEN  ! impossible condition here?
        p_new(i) = (0.0_RLK)
      ENDIF

      IF (p_new(i) < pmin) THEN
        p_new(i) = pmin
      ENDIF
    ENDDO

  END SUBROUTINE CalcPressureForElems



  SUBROUTINE  CalcEnergyForElems( domain, p_new,  e_new,  q_new,  &
                                  bvc,  pbvc,                     &
                                  p_old,  e_old,  q_old,          &
                                  compression,  compHalfStep,     &
                                  vnewc,  work,  delvc,  pmin,    &
                                  p_cut,   e_cut,  q_cut,  emin,  &
                                  qq,  ql,                        &
                                  rho0,                           &
                                  eosvmax,                        &
                                  length                          )
    IMPLICIT NONE 

    TYPE(domain_type), INTENT(INOUT) :: domain
    REAL(KIND=8), DIMENSION(0:) :: p_new, e_new, q_new
    REAL(KIND=8), DIMENSION(0:) :: bvc,  pbvc
    REAL(KIND=8), DIMENSION(0:) :: p_old, e_old, q_old
    REAL(KIND=8), DIMENSION(0:) :: compression, compHalfStep
    REAL(KIND=8), DIMENSION(0:) :: vnewc, work, delvc
    REAL(KIND=8)    :: pmin, p_cut,  e_cut, q_cut, emin
    REAL(KIND=8), DIMENSION(0:) :: qq, ql
    INTEGER(KIND=4), PARAMETER :: RLK = 8
    REAL(KIND=8)    :: rho0
    REAL(KIND=8)    :: eosvmax
    INTEGER(KIND=4) :: length

    INTEGER(KIND=4) :: i, ielem
    REAL(KIND=8)    :: vhalf, ssc, q_tilde
    REAL(KIND=8), DIMENSION(:), ALLOCATABLE :: pHalfStep
    REAL(KIND=8), PARAMETER :: TINY1 = 0.111111e-36_RLK
    REAL(KIND=8), PARAMETER :: TINY3 = 0.333333e-18_RLK
    REAL(KIND=8), PARAMETER :: SIXTH = (1.0_RLK) / (6.0_RLK)


    ALLOCATE(pHalfStep(0:length-1))

!$OMP PARALLEL DO PRIVATE(i) DEFAULT(none)   &
!$OMP SHARED(e_new, e_old, delvc, p_old, q_old, work, emin)
    DO i = 0, length-1
      e_new(i) = e_old(i) - (0.5_RLK) * delvc(i) * (p_old(i) + q_old(i))  &
               + (0.5_RLK) * work(i)

      IF (e_new(i)  < emin ) THEN
        e_new(i) = emin
      ENDIF
    ENDDO

    CALL CalcPressureForElems(domain, pHalfStep, bvc, pbvc, e_new, &
                              compHalfStep, vnewc, pmin, p_cut,    &
                              eosvmax, length)

!$OMP PARALLEL DO PRIVATE(i, vhalf, ssc) DEFAULT(none)              &
!$OMP SHARED(compHalfStep, delvc, q_new, pbvc, e_new, vhalf, bvc,   &
!$OMP        pHalfStep, rho0, ql, qq, p_old, q_old, TINY1, TINY3)
    DO i = 0, length-1
      vhalf = (1.0_RLK) / ((1.0_RLK) + compHalfStep(i))

      IF ( delvc(i) > (0.0_RLK) ) THEN
        q_new(i) = (0.0_RLK)
      ELSE
        ssc = (pbvc(i) * e_new(i) +   &
               vhalf * vhalf * bvc(i) * pHalfStep(i)) / rho0

        IF ( ssc <= TINY1 ) THEN
          ssc = TINY3
        ELSE
          ssc = SQRT(ssc)
        ENDIF

        q_new(i) = (ssc*ql(i) + qq(i))
      ENDIF

      e_new(i) = e_new(i) + (0.5_RLK) * delvc(i)  *  &
           (  (3.0_RLK)*(p_old(i)     + q_old(i)) -  &
                (4.0_RLK)*(pHalfStep(i) + q_new(i)))
    ENDDO

!$OMP PARALLEL DO PRIVATE(i) DEFAULT(none) SHARED(e_new, work, e_cut, emin)
    DO i = 0, length-1
      e_new(i) = e_new(i) + (0.5_RLK) * work(i)

      IF (ABS(e_new(i)) < e_cut) THEN
        e_new(i) = (0.0_RLK)
      ENDIF
      IF (e_new(i)  < emin ) THEN
        e_new(i) = emin
      ENDIF
    ENDDO

    CALL CalcPressureForElems(domain, p_new, bvc, pbvc, e_new, &
                              compression, vnewc, pmin, p_cut, &
                              eosvmax, length)

!$OMP PARALLEL DO PRIVATE(i, ielem, q_tilde, ssc) DEFAULT(none)            &
!$OMP SHARED(domain, delvc, pbvc, e_new, vnewc, bvc, p_new, rho0, TINY1, TINY3,   &
!$OMP        ql, qq, p_old, q_old, pHalfStep, q_new, delvc, e_cut, emin)
    DO i = 0, length-1
      ielem = domain%m_regElemlist(i)

      IF (delvc(i) > (0.0_RLK)) THEN
        q_tilde = (0.0_RLK)
      ELSE
        ssc = ( pbvc(i) * e_new(i)         &
            + vnewc(ielem) * vnewc(ielem)  &
            * bvc(i) * p_new(i) ) / rho0

        IF ( ssc <= TINY1 ) THEN
          ssc = TINY3
        ELSE
          ssc = SQRT(ssc)
        ENDIF

        q_tilde = (ssc*ql(i) + qq(i))
      ENDIF

      e_new(i) = e_new(i) - (  (7.0_RLK)*(p_old(i)     + q_old(i))   &
                          -    (8.0_RLK)*(pHalfStep(i) + q_new(i))   &
                          + (p_new(i) + q_tilde)) * delvc(i)*SIXTH

      IF (ABS(e_new(i)) < e_cut) THEN
        e_new(i) = (0.0_RLK)
      ENDIF
      IF ( e_new(i)  < emin ) THEN
        e_new(i) = emin
      ENDIF
    ENDDO

    CALL CalcPressureForElems(domain, p_new, bvc, pbvc, e_new, &
                              compression, vnewc, pmin, p_cut, &
                              eosvmax, length)

!$OMP PARALLEL DO PRIVATE(i, ielem, ssc) DEFAULT(none)              &
!$OMP SHARED(domain, delvc, pbvc, e_new, vnewc, bvc, p_new, rho0,   &
!$OMP        TINY1, TINY3, ql, qq, q_new, q_cut)
    DO i = 0, length-1
      ielem = domain%m_regElemlist(i)

      IF ( delvc(i) <= (0.0_RLK) ) THEN
        ssc = ( pbvc(i) * e_new(i)        &
            + vnewc(ielem) * vnewc(ielem) &
            * bvc(i) * p_new(i) ) / rho0

        IF ( ssc <= TINY1 ) THEN
          ssc = TINY3
        ELSE
          ssc = SQRT(ssc)
        ENDIF

        q_new(i) = (ssc*ql(i) + qq(i))

        IF (ABS(q_new(i)) < q_cut) THEN
          q_new(i) = (0.0_RLK)
        ENDIF
      ENDIF
    ENDDO

    DEALLOCATE(pHalfStep)

  END SUBROUTINE CalcEnergyForElems



  SUBROUTINE CalcSoundSpeedForElems(domain, vnewc,  rho0, enewc, &
                                    pnewc, pbvc,         &
                                    bvc, ss4o3, numElem       )
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    REAL(KIND=8), DIMENSION(0:) :: vnewc, enewc
    REAL(KIND=8), DIMENSION(0:) :: pnewc, pbvc
    REAL(KIND=8), DIMENSION(0:) :: bvc
    INTEGER(KIND=4), PARAMETER :: RLK = 8
    REAL(KIND=8) :: rho0
    REAL(KIND=8) :: ss4o3
    INTEGER      :: numElem
    REAL(KIND=8), PARAMETER :: TINY1 = 0.111111e-36_RLK
    REAL(KIND=8), PARAMETER :: TINY3 = 0.333333e-18_RLK
    REAL(KIND=8) :: ssTmp
    INTEGER      :: i, ielem

!$OMP PARALLEL DO PRIVATE(i, ielem, ssTmp) DEFAULT(none)    &
!$OMP SHARED(domain, pbvc, enewc, vnewc, bvc, pnewc, rho0, TINY1, TINY3)
    DO i=0, numElem-1
      ielem = domain%m_regElemlist(i)
      ssTmp = (pbvc(i) * enewc(i)           &
              + vnewc(ielem) * vnewc(ielem) &
              * bvc(i) * pnewc(i)) / rho0
      IF (ssTmp <= TINY1) THEN
        ssTmp = TINY3
      ELSE
        ssTmp = SQRT(ssTmp)
      ENDIF
      domain%m_ss(ielem) = ssTmp
    ENDDO

  END SUBROUTINE CalcSoundSpeedForElems



  SUBROUTINE EvalEOSForElems(domain, vnewc, length, rep)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    REAL(KIND=8), DIMENSION(0:) :: vnewc
    INTEGER :: length
    INTEGER(KIND=4), PARAMETER :: RLK = 8
    INTEGER(KIND=4) :: rep

    REAL(KIND=8) :: e_cut
    REAL(KIND=8) :: ss4o3
    REAL(KIND=8) :: q_cut
    REAL(KIND=8) :: p_cut
    REAL(KIND=8) :: eosvmax
    REAL(KIND=8) :: eosvmin
    REAL(KIND=8) :: pmin
    REAL(KIND=8) :: emin
    REAL(KIND=8) :: rho0
    REAL(KIND=8) :: vchalf
    REAL(KIND=8), DIMENSION(:), ALLOCATABLE :: e_old, &
                  delvc, p_old, q_old, compression,   &
                  compHalfStep, qq, ql, work, p_new,  &
                  e_new, q_new, bvc, pbvc
    INTEGER      :: i, j, ielem

    e_cut = domain%m_e_cut 
    p_cut = domain%m_p_cut
    ss4o3 = domain%m_ss4o3
    q_cut = domain%m_q_cut
    eosvmax = domain%m_eosvmax
    eosvmin = domain%m_eosvmin
    pmin = domain%m_pmin
    emin = domain%m_emin
    rho0 = domain%m_refdens

    ALLOCATE(e_old(0:length-1))
    ALLOCATE(delvc(0:length-1))
    ALLOCATE(p_old(0:length-1))
    ALLOCATE(q_old(0:length-1))
    ALLOCATE(compression(0:length-1))
    ALLOCATE(compHalfStep(0:length-1))
    ALLOCATE(qq(0:length-1))
    ALLOCATE(ql(0:length-1))
    ALLOCATE(work(0:length-1))
    ALLOCATE(p_new(0:length-1))
    ALLOCATE(e_new(0:length-1))
    ALLOCATE(q_new(0:length-1))
    ALLOCATE(bvc(0:length-1))
    ALLOCATE(pbvc(0:length-1))

    ! Loop to add load imbalance based on region number
    DO j=0, rep-1
      ! compress data, minimal set
!$OMP PARALLEL DO PRIVATE(i, ielem) DEFAULT(none)    &
!$OMP SHARED(domain, e_old, delvc, p_old, q_old, qq, ql)
      DO i = 0, length-1
        ielem = domaiN%m_regElemlist(i)
        e_old(i) = domain%m_e(ielem)
        delvc(i) = domain%m_delv(ielem)
        p_old(i) = domain%m_p(ielem)
        q_old(i) = domain%m_q(ielem)
        qq(i) = domain%m_qq(ielem)
        ql(i) = domain%m_ql(ielem)
      ENDDO

!$OMP PARALLEL DO PRIVATE(i, ielem, vchalf) DEFAULT(none)   &
!$OMP SHARED(domain, compression, vnewc, delvc, compHalfStep)
      DO i = 0, length-1
        ielem = domain%m_regElemlist(i)
        compression(i) = (1.0_RLK) / vnewc(ielem) - (1.0_RLK)
        vchalf = vnewc(ielem) - delvc(i) * (0.5_RLK)
        compHalfStep(i) = (1.0_RLK) / vchalf - (1.0_RLK)
      ENDDO

      ! Check for v > eosvmax or v < eosvmin
      IF ( eosvmin /= (0.0_RLK) ) THEN
!$OMP PARALLEL DO PRIVATE(i, ielem) DEFAULT(none)   &
!$OMP SHARED(domain, vnewc, eosvmin, compHalfStep, compression)
        DO i = 0, length-1
          ielem = domain%m_regElemlist(i)
          IF (vnewc(ielem) <= eosvmin) THEN  ! impossible due to calling func?
            compHalfStep(i) = compression(i)
          ENDIF
        ENDDO
      ENDIF
      IF ( eosvmax /= (0.0_RLK) ) THEN
!$OMP PARALLEL DO PRIVATE(i, ielem) DEFAULT(none)   &
!$OMP SHARED(domain, vnewc, eosvmax, p_old, compression, compHalfStep)
        DO i = 0, length-1
          ielem = domain%m_regElemlist(i)
          IF (vnewc(ielem) >= eosvmax) THEN ! impossible due to calling func? 
            p_old(i)        = (0.0_RLK)
            compression(i)  = (0.0_RLK)
            compHalfStep(i) = (0.0_RLK)
          ENDIF
        ENDDO
      ENDIF

!$OMP PARALLEL DO PRIVATE(i) DEFAULT(none) SHARED(work)
      DO i = 0, length-1
        work(i) = (0.0_RLK)
      ENDDO
    ENDDO

    CALL CalcEnergyForElems(domain, p_new, e_new, q_new, bvc, pbvc,  &
                            p_old, e_old,  q_old, compression,       &
                            compHalfStep, vnewc, work,  delvc, pmin, &
                            p_cut, e_cut, q_cut, emin,               &
                            qq, ql, rho0, eosvmax, length)

    ! Watch out: Scoping in C++ might produce weird errors here!!

!$OMP PARALLEL DO PRIVATE(i, ielem) DEFAULT(none)   &
!$OMP SHARED(domain, p_new, e_new, q_new)
    DO i = 0, length-1
      ielem = domain%m_regElemlist(i)
      domain%m_p(ielem) = p_new(i)
      domain%m_e(ielem) = e_new(i)
      domain%m_q(ielem) = q_new(i)
    ENDDO

    CALL CalcSoundSpeedForElems(domain, vnewc, rho0, e_new, p_new,  &
                                pbvc, bvc, ss4o3, length)

    DEALLOCATE(pbvc)
    DEALLOCATE(bvc)
    DEALLOCATE(q_new)
    DEALLOCATE(e_new)
    DEALLOCATE(p_new)
    DEALLOCATE(work)
    DEALLOCATE(ql)
    DEALLOCATE(qq)
    DEALLOCATE(compHalfStep)
    DEALLOCATE(compression)
    DEALLOCATE(q_old)
    DEALLOCATE(p_old)
    DEALLOCATE(delvc)
    DEALLOCATE(e_old)


  END SUBROUTINE EvalEOSForElems



  SUBROUTINE ApplyMaterialPropertiesForElems(domain)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    REAL(KIND=8) :: eosvmin
    REAL(KIND=8) :: eosvmax
    REAL(KIND=8) :: vc
    REAL(KIND=8), DIMENSION(:), ALLOCATABLE :: vnewc
    INTEGER(KIND=4) :: length, numElemReg
    INTEGER(KIND=4), POINTER :: ielem
    INTEGER(KIND=4) :: i, r
    INTEGER(KIND=4), PARAMETER :: RLK = 8
    INTEGER(KIND=4) :: rep

    ! Hacky hacky
    INTEGER(KIND=4), PARAMETER :: VolumeError = -1

    eosvmin = domain%m_eosvmin
    eosvmax = domain%m_eosvmax
    length = domain%m_numElem

    IF (length /= 0) THEN
      ! Expose all of the variables needed for material evaluation
      ALLOCATE(vnewc(0:length-1))

!$OMP PARALLEL DO PRIVATE(i) DEFAULT(none) SHARED(domain, vnewc)
      DO i = 0, length-1
        !CALL __ENZYME_INTEGER(domain%m_matElemlist(i))
        vnewc(i) = domain%m_vnew(i)
      ENDDO

      IF (eosvmin /= (0.0_RLK)) THEN
!$OMP PARALLEL DO PRIVATE(i) DEFAULT(none) SHARED(vnewc, eosvmin)
        DO i = 0, length-1
          IF (vnewc(i) < eosvmin) THEN
            vnewc(i) = eosvmin
          ENDIF
        ENDDO
      ENDIF

      IF (eosvmax /= (0.0_RLK)) THEN
!$OMP PARALLEL DO PRIVATE(i) DEFAULT(none) SHARED(vnewc, eosvmax)
        DO i = 0, length-1
          IF (vnewc(i) > eosvmax) THEN
            vnewc(i) = eosvmax
          ENDIF
        ENDDO
      ENDIF

      ! This check may not make perfect sense in LULESH, but
      ! it's representative of something in the full code -
      ! just leave it in, please
!$OMP PARALLEL DO PRIVATE(i, vc) DEFAULT(none)   &
!$OMP SHARED(domain, eosvmin, eosvmax, VolumeError)
      DO i = 0, length-1
        !CALL __ENZYME_INTEGER(domain%m_matElemlist(i))
        vc = domain%m_v(i)
        IF (eosvmin /= (0.0_RLK)) THEN
          IF (vc < eosvmin) THEN
            vc = eosvmin
          ENDIF
        ENDIF
        IF (eosvmax /= (0.0_RLK)) THEN
          IF (vc > eosvmax) THEN
            vc = eosvmax
          ENDIF
        ENDIF
        IF (vc <= 0.0_RLK) THEN
          CALL luabort(VolumeError)
        ENDIF
      ENDDO
    ENDIF

    DO r=0, domain%m_numReg-1
      numElemReg = domain%m_regElemSize(r)

      ! Determine load imbalance for this region
      ! round down the number with lowest cost
      IF (r < domain%m_numReg/2) THEN
        rep = 1
      ELSE IF (r < (domain%m_numReg - (domain%m_numReg + 15)/20)) THEN
        rep = 1 + domain%m_cost
      ELSE
        rep = 10 * (1 + domain%m_cost)
      ENDIF

      CALL EvalEOSForElems(domain, vnewc, length, rep)
    ENDDO

    DEALLOCATE(vnewc)

  END SUBROUTINE ApplyMaterialPropertiesForElems



  SUBROUTINE UpdateVolumesForElems(domain)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    REAL(KIND=8)    :: v_cut
    REAL(KIND=8)    :: tmpV
    INTEGER(KIND=4) :: numElem
    INTEGER(KIND=4) :: i
    INTEGER(KIND=4), PARAMETER :: RLK = 8

    v_cut = domain%m_v_cut
    numElem = domain%m_numElem

    IF (numElem /= 0) THEN
!$OMP PARALLEL DO PRIVATE(i, tmpV) DEFAULT(none) SHARED(domain, v_cut)
      DO i = 0, numElem - 1
        tmpV = domain%m_vnew(i)

        IF ( ABS(tmpV - (1.0_RLK)) < v_cut ) THEN
          tmpV = (1.0_RLK)
        ENDIF
        domain%m_v(i) = tmpV
      ENDDO
    ENDIF

  END SUBROUTINE UpdateVolumesForElems



  SUBROUTINE LagrangeElements(domain)
    IMPLICIT NONE 

    TYPE(domain_type), INTENT(INOUT) :: domain

    CALL CalcLagrangeElements(domain)

    ! Calculate Q.  (Monotonic q option requires communication)
    CALL CalcQForElems(domain)

    CALL ApplyMaterialPropertiesForElems(domain)

    CALL UpdateVolumesForElems(domain)

  END SUBROUTINE LagrangeElements



  SUBROUTINE CalcCourantConstraintForElems(domain)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    REAL(KIND=8)    :: dtcourant
    INTEGER(KIND=4) :: COURANT_ELEM
    INTEGER(KIND=4) :: threads, i
    INTEGER(KIND=4), PARAMETER :: RLK = 8

    REAL(KIND=8) :: qqc, dtf

    INTEGER(KIND=4), DIMENSION(:), ALLOCATABLE :: courant_elem_per_thread
    REAL(KIND=8),    DIMENSION(:), ALLOCATABLE :: dtcourant_per_thread

    INTEGER(KIND=4) :: length
    REAL(KIND=8) :: qqc2
    REAL(KIND=8) :: dtcourant_tmp
    INTEGER(KIND=4) :: indx, thread_num


    length = domain%m_numElem
    
#if _OPENMP
      threads = OMP_GET_MAX_THREADS()
      ALLOCATE(dtcourant_per_thread(0:threads-1))
      ALLOCATE(courant_elem_per_thread(0:threads-1))
#else
      threads = 1_4
      ALLOCATE(dtcourant_per_thread(0:threads-1))
      ALLOCATE(courant_elem_per_thread(0:threads-1))
#endif

    qqc = domain%m_qqc

!$OMP PARALLEL PRIVATE(qqc2, dtcoutran_tmp, courant_elem, thread_num, i, indx, dtf)  &
!$OMP DEFAULT(none) SHARED(domain, qqc, length, dtcourant_per_thread,                &
!$OMP                      courant_elem_per_thread)
    qqc2 = (64.0_RLK) * qqc * qqc

    dtcourant_tmp = domain%m_dtcourant  ! TODO(Ludger): Does this need to be a pointer?
    COURANT_ELEM = -1

#if _OPENMP
      thread_num = OMP_GET_THREAD_NUM()
#else
      thread_num = 0_4
#endif

!$OMP DO
    DO i = 0, length-1
      indx = domain%m_regElemlist(i)
      dtf = domain%m_ss(indx) * domain%m_ss(indx)

      IF ( domain%m_vdov(indx) < (0.0_RLK) ) THEN
        dtf = dtf + qqc2 * domain%m_arealg(indx) * domain%m_arealg(indx)  &
                  * domain%m_vdov(indx) * domain%m_vdov(indx)
      ENDIF

      dtf = SQRT(dtf)
      dtf = domain%m_arealg(indx) / dtf

      ! Determine minimum timestep with its corresponding elem
      IF (domain%m_vdov(indx) /= (0.0_RLK)) THEN
        IF ( dtf < dtcourant_tmp ) THEN
          dtcourant_tmp = dtf
          COURANT_ELEM = indx
        ENDIF
      ENDIF
    ENDDO

    dtcourant_per_thread(thread_num) = dtcourant_tmp
    courant_elem_per_thread(thread_num) = courant_elem
!$OMP END PARALLEL

    DO i = 1, threads-1
      IF(dtcourant_per_thread(i) < dtcourant_per_thread(0)) THEN
        dtcourant_per_thread(0) = dtcourant_per_thread(i)
        courant_elem_per_thread(0) =  courant_elem_per_thread(i)
      ENDIF
    ENDDO

    ! Don't try to register a time constraint if none of the elements
    ! were active
    IF (courant_elem_per_thread(0) /= -1) THEN
      domain%m_dtcourant = dtcourant_per_thread(0)
    ENDIF

    RETURN

  END SUBROUTINE CalcCourantConstraintForElems



  SUBROUTINE CalcHydroConstraintForElems(domain)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    REAL(KIND=8) :: dthydro
    REAL(KIND=8) :: dvovmax, dtdvov
    INTEGER(KIND=4), PARAMETER :: RLK = 8
    REAL(KIND=8), DIMENSION(:), ALLOCATABLE :: dthydro_per_thread
    INTEGER(KIND=4), DIMENSION(:), ALLOCATABLE :: hydro_elem_per_thread
    INTEGER(KIND=4) :: hydro_elem
    INTEGER(KIND=4) :: threads, length
    INTEGER(KIND=4) :: indx, thread_num, i
    REAL(KIND=8) :: dthydro_tmp

#if _OPENMP
      threads = OMP_GET_MAX_THREADS()
      ALLOCATE(dthydro_per_thread(0:threads-1))
      ALLOCATE(hydro_elem_per_thread(0:threads-1))
#else
      threads = 1
      ALLOCATE(dthydro_per_thread(0:threads-1))
      ALLOCATE(hydro_elem_per_thread(0:threads-1))
#endif

    dvovmax = domain%m_dvovmax
    length = domain%m_numElem

    ! CALL __ENZYME_INTEGER(hydro_elem)
!$OMP PARALLEL PRIVATE(dthydro_tmp, hydro_elem, thread_num, i, indx, dtdvov)   &
!$OMP DEFAULT(none) SHARED(domain, dvovmax, dthydro_per_thread, hydro_elem_per_thread)
    dthydro_tmp = domain%m_dthydro
    hydro_elem = -1

#if _OPENMP
      thread_num = OMP_GET_THREAD_NUM()
#else
      thread_num = 0
#endif
    dthydro_tmp = domain%m_dthydro
    hydro_elem = -1

!$OMP DO
    DO i = 0, length-1
      indx = domain%m_regElemlist(i)
      !CALL __ENZYME_INTEGER(domain%m_matElemlist(i))
      IF (domain%m_vdov(indx) /= (0.0_RLK)) THEN
        dtdvov = dvovmax / (ABS(domain%m_vdov(indx))+(1.e-20_RLK))
        IF ( dthydro_tmp > dtdvov ) THEN
          dthydro_tmp = dtdvov
          hydro_elem = indx
        ENDIF
      ENDIF
    ENDDO

    dthydro_per_thread(thread_num) = dthydro_tmp
    hydro_elem_per_thread(thread_num) = hydro_elem
!$OMP END PARALLEL

    DO i = 1, threads-1
      IF (dthydro_per_thread(i) < dthydro_per_thread(0)) THEN
        dthydro_per_thread(0) = dthydro_per_thread(i)
        hydro_elem_per_thread(0) =  hydro_elem_per_thread(i)
      ENDIF
    ENDDO

    IF (hydro_elem_per_thread(0) /= -1) THEN
      domain%m_dthydro = dthydro_per_thread(0)
    ENDIF

    RETURN
  END SUBROUTINE CalcHydroConstraintForElems



  SUBROUTINE CalcTimeConstraintsForElems(domain)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain
    INTEGER(KIND=4), PARAMETER :: RLK = 8
    INTEGER :: r

    ! Initialize conditions to a very large value
    domain%m_dtcourant = 1.0e+20_RLK
    domain%m_dthydro = 1.0e+20_RLK

    DO r=0, domain%m_numReg-1
      ! Evaluate time constraint
      CALL CalcCourantConstraintForElems(domain)  ! TODO(Ludger): Check the region separation

      ! Check hydro constraint
      CALL CalcHydroConstraintForElems(domain)
    ENDDO

  END SUBROUTINE CalcTimeConstraintsForElems



  SUBROUTINE LagrangeLeapFrog(domain)
    IMPLICIT NONE

    TYPE(domain_type), INTENT(INOUT) :: domain

    ! calculate nodal forces, accelerations, velocities, positions, with
    ! applied boundary conditions and slide surface considerations
    CALL LagrangeNodal(domain)

    ! calculate element quantities (i.e. velocity gradient & q), and update
    ! material states
    CALL LagrangeElements(domain)
    CALL CalcTimeConstraintsForElems(domain)

  ! CALL LagrangeRelease()  ! Creation/destruction of temps may be important to capture 

  END SUBROUTINE LagrangeLeapFrog



  SUBROUTINE luabort(errcode)
    IMPLICIT NONE
    INTEGER(KIND=4) :: errcode
    WRITE(6,*) "ERROR CODE: ", errcode
    WRITE(6,*) "ABORTING"
    STOP
  END SUBROUTINE luabort



  REAL(KIND=8) FUNCTION CBRT(dat)

    IMPLICIT NONE
    REAL(KIND=8) :: dat
    INTEGER(KIND=4), PARAMETER :: RLK = 8

    CBRT = dat**(1.0_RLK/3.0_RLK)

  END FUNCTION CBRT



  REAL(KIND=8) FUNCTION CalcElemVolume( x, y, z )

    IMPLICIT NONE
    REAL(KIND=8), DIMENSION(0:7) :: x, y, z
    INTEGER(KIND=4), PARAMETER :: RLK = 8
    REAL(KIND=8)  :: volume
    REAL(KIND=8), PARAMETER :: twelveth = (1.0_RLK)/(12.0_RLK)

    REAL(KIND=8) :: dx61
    REAL(KIND=8) :: dy61
    REAL(KIND=8) :: dz61

    REAL(KIND=8) :: dx70
    REAL(KIND=8) :: dy70
    REAL(KIND=8) :: dz70

    REAL(KIND=8) :: dx63
    REAL(KIND=8) :: dy63 
    REAL(KIND=8) :: dz63

    REAL(KIND=8) :: dx20 
    REAL(KIND=8) :: dy20
    REAL(KIND=8) :: dz20

    REAL(KIND=8) :: dx50 
    REAL(KIND=8) :: dy50
    REAL(KIND=8) :: dz50

    REAL(KIND=8) :: dx64
    REAL(KIND=8) :: dy64
    REAL(KIND=8) :: dz64

    REAL(KIND=8) :: dx31
    REAL(KIND=8) :: dy31 
    REAL(KIND=8) :: dz31

    REAL(KIND=8) :: dx72
    REAL(KIND=8) :: dy72
    REAL(KIND=8) :: dz72

    REAL(KIND=8) :: dx43 
    REAL(KIND=8) :: dy43
    REAL(KIND=8) :: dz43

    REAL(KIND=8) :: dx57
    REAL(KIND=8) :: dy57
    REAL(KIND=8) :: dz57

    REAL(KIND=8) :: dx14
    REAL(KIND=8) :: dy14 
    REAL(KIND=8) :: dz14

    REAL(KIND=8) :: dx25
    REAL(KIND=8) :: dy25 
    REAL(KIND=8) :: dz25

    volume = 0.0_RLK

    dx61 = x(6) - x(1)
    dy61 = y(6) - y(1)
    dz61 = z(6) - z(1)

    dx70 = x(7) - x(0)
    dy70 = y(7) - y(0)
    dz70 = z(7) - z(0)

    dx63 = x(6) - x(3)
    dy63 = y(6) - y(3)
    dz63 = z(6) - z(3)

    dx20 = x(2) - x(0)
    dy20 = y(2) - y(0)
    dz20 = z(2) - z(0)

    dx50 = x(5) - x(0)
    dy50 = y(5) - y(0)
    dz50 = z(5) - z(0)

    dx64 = x(6) - x(4)
    dy64 = y(6) - y(4)
    dz64 = z(6) - z(4)

    dx31 = x(3) - x(1)
    dy31 = y(3) - y(1)
    dz31 = z(3) - z(1)

    dx72 = x(7) - x(2)
    dy72 = y(7) - y(2)
    dz72 = z(7) - z(2)

    dx43 = x(4) - x(3)
    dy43 = y(4) - y(3)
    dz43 = z(4) - z(3)

    dx57 = x(5) - x(7)
    dy57 = y(5) - y(7)
    dz57 = z(5) - z(7)

    dx14 = x(1) - x(4)
    dy14 = y(1) - y(4)
    dz14 = z(1) - z(4)

    dx25 = x(2) - x(5)
    dy25 = y(2) - y(5)
    dz25 = z(2) - z(5)

    volume =  TRIPLE_PRODUCT(dx31 + dx72, dx63, dx20,   &
                             dy31 + dy72, dy63, dy20,   &
                             dz31 + dz72, dz63, dz20) + &
              TRIPLE_PRODUCT(dx43 + dx57, dx64, dx70,   &
                             dy43 + dy57, dy64, dy70,   &
                             dz43 + dz57, dz64, dz70) + &
              TRIPLE_PRODUCT(dx14 + dx25, dx61, dx50,   &
                             dy14 + dy25, dy61, dy50,   &
                             dz14 + dz25, dz61, dz50)

    volume = volume*twelveth

    CalcElemVolume=volume
    RETURN

  END FUNCTION CalcElemVolume



  REAL(KIND=8) FUNCTION TRIPLE_PRODUCT(x1, y1, z1, x2, y2, z2, x3, y3, z3)

    REAL(KIND=8) :: x1, y1, z1, x2, y2, z2, x3, y3, z3

    TRIPLE_PRODUCT = ((x1)*((y2)*(z3) - (z2)*(y3)) + (x2)*((z1)*(y3)  &
                    - (y1)*(z3)) + (x3)*((y1)*(z2) - (z1)*(y2)))

    RETURN

  END FUNCTION TRIPLE_PRODUCT



  FUNCTION AreaFace( x0, x1, x2, x3,  &
                   y0, y1, y2, y3,  &
                   z0, z1, z2, z3  ) RESULT(area)


    IMPLICIT NONE
    REAL(KIND=8)  :: x0, x1, x2, x3
    REAL(KIND=8)  :: y0, y1, y2, y3
    REAL(KIND=8)  :: z0, z1, z2, z3

    REAL(KIND=8) :: fx, fy, fz
    REAL(KIND=8) :: gx, gy, gz
    REAL(KIND=8) :: area

    fx = (x2 - x0) - (x3 - x1)
    fy = (y2 - y0) - (y3 - y1)
    fz = (z2 - z0) - (z3 - z1)
    gx = (x2 - x0) + (x3 - x1)
    gy = (y2 - y0) + (y3 - y1)
    gz = (z2 - z0) + (z3 - z1)

    area =                             &
      (fx * fx + fy * fy + fz * fz) *  &
      (gx * gx + gy * gy + gz * gz) -  &
      (fx * gx + fy * gy + fz * gz) *  &
      (fx * gx + fy * gy + fz * gz)

    RETURN

  END FUNCTION AreaFace

END MODULE
